static gxSurf ofs = gxSurf.create(512, 512, 32);

vec4f blob(vec4f in) {
	float64 max(float64 a, float64 b) {
		if (a > b) {
			return a;
		}
		return b;
	}
	float64 clamp(float64 t, float64 a, float64 b) {
		if (t < a) {
			return a;
		}
		if (t > b) {
			return b;
		}
		return t;
	}

	double time = in.w / 2;
	vec2d p = vec2d(in.x, in.y);

	//! the centre point of each blob
	vec2d move1 = vec2d(double.cos(time * 4.5) * 0.4, double.sin(time * 1.5) * 0.4);
	vec2d move2 = vec2d(double.cos(time * 2.0) * 0.4, double.sin(time * 3.0) * 0.4);
	vec2d move3 = vec2d(double.cos(time * 7.0) * 0.4, double.sin(time * 2.0) * 0.4);
	vec2d move4 = vec2d(double.cos(time * 2.0) * 0.4, double.sin(time * 4.3) * 0.4);

	double metaball = 0;
	//! sum the meatballs as the radius for each blob
	metaball += 1. / max(1, dot(sub(p, move1), sub(p, move1)) * 22);
	metaball += 1. / max(1, dot(sub(p, move2), sub(p, move2)) * 26);
	metaball += 1. / max(1, dot(sub(p, move3), sub(p, move3)) * 32);
	metaball += 1. / max(1, dot(sub(p, move4), sub(p, move4)) * 32);


	//~ alter the cut-off power
	metaball = clamp(float64.pow(metaball, 8), 0, 1);
	return vec4f(float32(metaball));
}

int onEvent(int action, int button, int ex, int ey) {
	inline resX = 127;
	inline resY = 127;
	inline aspect = resX / double(resY);

	static int64 start = System.millis();
	static gxSurf thumb = gxSurf.create(resX, resY, 32);

	if (action == Gui.WINDOW_CLOSE) {
		trace("closing ...");
		return 0;
	}
	if (action == Gui.KEY_PRESS) {
		if (button == 27) {
			return -1;
		}
	}

	vec4f in = {
		x: 0;
		y: 0;
		z: 0;
		w: (System.millis() - start) / 1000f;
	};
	for (int iy = 0; iy < resY; iy += 1) {
		in.y = 2 * iy / double(resY) - 1;
		for (int ix = 0; ix < resX; ix += 1) {
			in.x = 2 * aspect * ix / double(resX) - 1;
			gxSurf.set(thumb, ix, iy, rgb(blob(in)));
		}
	}
	gxSurf.zoomSurf(ofs, null, thumb, null, 1);

	return 0;
}

Gui.showWindow(ofs, onEvent);
