# Declarations
<!--outline:
	Aliasing {#}
		Type aliasing
		Constant aliasing
		Expression aliasing
	Enumerations {E}
		Iterating enumeration
		Indexing the enumeration class
		Anonymous enumerations
		Implementation notes
	Structs and Unions {S}
	Variables and Functions {&}
		Initialization
	Operators and Properties {Â±}
		Type conversion operator
		Unary, binary operators
		Property operators
-->

<!-- TODO?

assigning to an array of base type?
	vec3f enumValues[] = coord;
-->

Every declaration (typename, function or variable) results in a variable declaration and definition.

## Aliasing
Aliasing is an analogue to c preprocessor define, except it is expanded on syntax tree level, and the arguments may be
evaluated only once.

**Syntax:**

```
('static' | 'const')* 'inline' name ('(' args ')')? '=' expr;
```

### Type aliasing

**Example:**

```
inline double = float64;
```

### Constant aliasing

**Example:**

```
inline pi = 3.14;
```

### Expression aliasing

**Example:**

```
inline min(int a, int b) = a < b ? a : b;
```

- the right hand side can also use local variables if static is not used
- static should force the right side not to use local variables
- const should force the right side to be constant

## Enumerations

An enumeration declares a list of named constants all of a given type.

**Syntax:**

```
'enum' NAME? (':' typename)? '{' (NAME initializer? ';')* '}';
```

It may also add a new type, the enumeration type, if the name is specified.

The enumeration type is a reference type, and its instances may be assigned only with null or the values listed inside the enumeration.

The type of the enumerated values can be specified. If not specified the type of elements of the enum will be int.

Every enumeration variable declaration must be initialized.

**Example:**

```
struct vec3f {
	float x;
	float y;
	float z;
}

enum coord: vec3f {
	x = vec3f(1, 0, 0);
	y = vec3f(0, 1, 0);
	z = vec3f(0, 0, 1);
}

coord e1 = coord.x;             // ok: enum variable can be assigned with a value from the enumeration.
coord e2 = e1;                  // ok: enum variable can be assigned with the same type of variable.
e2 = coord.x;                   // ok.
e2 = e1;                        // ok.

vec3f v1 = coord.y;             // ok: values from enum are of type vec3f, so it can be assigned.
vec3f v2 = e1;                  // ok: enum variables can be assigned to variables of their base type.
v2 = coord.x;                   // ok.
v2 = e2;                        // ok.

coord e3 = v1;                  // error: enum variable can be assigned only from a value from the enumeration.
e3 = v1;                        // error.
coord e0;                       // error: enum variable must be initialized.
```

### Iterating enumeration

```
for (coord elem: coord) {
	...
}
```

### Indexing enumeration

if the index is an integer the enumeration on that position is returned.

```
assert(coord[2] === coord.y);
```

if the index is a string lookup by name is performed.

```
assert(coord["y"] === coord.y);
```


### Anonymous enumeration

An anonymous enumeration will not create a new type, it exposes the enumerated values to the declaring scope.

**Example:**

```
struct vec3f {
	float x;
	float y;
	float z;

	enum: vec3f {
		x = vec3f(1, 0, 0);
		y = vec3f(0, 1, 0);
		z = vec3f(0, 0, 1);
	}
}

vec3f v1 = vec3f.y;
```

### Implementation notes

- There are basically 2 different enumeration kinds: reference or value based.

	- when the base type is value type, the members of the enumeration will be ordered in the memory,
		like in an array. In this case the lookup is easy and can be done in constant time.

	- when the base type is reference type, ex strings or functions, the enumeration holds references to each member.
		lookup in this case must match each member in the enumeration, if references are unordered.


## Records

[wikipedia](https://en.wikipedia.org/wiki/Record_(computer_science))

**Syntax:**

```
('static')? 'struct' name? (':' (pack|extend))? '{' decl+ '}'
```

**Example:**

```
struct Complex {
	const double re;
	const double im = 0;
}
```

- structures may contain only instance or static variables.
- declaring a function in a struct results in a function reference, or a static function in case it is implemented.

**Example:**

```
struct a {
	int f1(int a, int b);
	int f2(int a, int b) = null;
	int f3(int a, int b) {
		return a < b ? a : b;
	}
}
```

- fields f1 and f2 are function reference instance members.
- f3 is converted to a static function, triggering a warning that it should be declared static.

### Static structures

Static declared structures will have all members static (sort of namespaces).
These types of structs will have the size 0, and can not be instantiated.
	the best example of its usage is: static struct Math {...}

### Packed structures

A struct can be packed with a small integer of a power of 2. [0, 1, 2, 4, 8, 16, 32].

When a struct is packed with 0 it becomes a c like union, meaning every member of it will start at the same memory location.

### Extended structures

Every type which is extended from another type, must have at its root the type object.

**Example:**

```
struct ComplexClass: object {
	const double re;
	const double im = 0;
}
```

### Generated initializer

When declaring a record there will be declared a default initializer with all
non static fields, in case the default packing is used, and there is at least one field.

**Example:**

```
inline Complex(double re, double im) = emit(Complex, double(re), double(im));
```


## Operators

Operators are close to the aliasing construct, and they work almost in the same way.
Operators just like aliasing are resolved at compile time,
and they can be expressed in terms of an expressions.
While inline is implicit, operator is explicit.

### Type conversion operator

**Syntax:**

```
'operator' type '(' type rhs ')' '=' expr ';'
```

**Example.Complex:**

```
operator Complex(float64 re) = Complex(re, 0);
Complex a = 9.;       // ok: operators are explicit.
```

**Example.Degrees:**

```
struct Celsius { double degrees; }
struct Fahrenheit { double degrees; }

// Celsius(double) and Fahrenheit(double) initializers are generated by the compiler.

// explicit type conversion using inlines
inline Celsius(Fahrenheit fahrenheit) = Celsius((fahrenheit.degrees - 32) * (5. / 9.));
inline Fahrenheit(Celsius celsius) = Fahrenheit((celsius.degrees + 32) * (9. / 5.));

// implicit type conversion using operators
operator Celsius(Fahrenheit fahrenheit) = Celsius(fahrenheit);
operator Fahrenheit(Celsius celsius) = Fahrenheit(celsius);

Celsius boilC = Celsius(100.);
Fahrenheit boilF_implicit = Fahrenheit(boilC);          // executes inline
Fahrenheit boilF_explicit = boilC;                      // executes operator
```

### Unary and binary operators

**Syntax:**

```
// unary operator overloading
'operator' operator '(' type rhs ')' '=' expr ';'
// binary operator overloading
'operator' operator '(' type lhs ',' type rhs ')' '=' expr ';'
'operator' '(' type lhs ')' operator '(' type rhs ')' '=' expr ';'
```

**Example:**

```
operator - (Complex a) = Complex(-a.re, -a.im);
Complex b = -a;

operator (Complex a) + (Complex b) = Complex(a.re + b.re, a.im + b.im);
Complex sum = a + b;
```

### Property operators

**Syntax:**

```
// getter
'operator' '(' type rhs ')' '.' name '=' expr ';'
// method
'operator' '(' type rhs ')' '.' name '(' args ')' '=' expr ';'
// indexer
'operator' '(' type rhs ')' '[' args ']' '=' expr ';'
// invoker
'operator' '(' type rhs ')' '(' args ')' '=' expr ';'
// setters
probably not a good idea, and not really use full, getter returning an rvalue is assignable.
```

**Example.Complex:**

```
Complex a = 3.                          // 3 + 0 * i
Complex b = Complex(5, 1);              // 5 + 1 * i

operator (Complex a).abs = Math.hypot(a.re, a.im);
double x = a.abs;

operator (Complex a).add(Complex b) = Complex(a.re + b.re, a.im + b.im);
Complex sum = a.add(b);

operator (Complex c)[int idx] = float64(idx == 0 ? c.re : idx == 1 ? c.im : Math.Nan);
operator (Complex c)[string idx] = float64(idx == "re" ? c.re : idx == "im" ? c.im : Math.Nan);

operator (Complex c)(int idx) = c[i];

float64 re = a[0];        // => operator (Complex c)[int idx]
float64 im = a["im"];     // => operator (Complex c)[string idx]
float64 re2 = a(0);       // => operator (Complex c)(int idx)
```

**Example.Graphics:**

```
// getter
operator (gxSurf src).width = gxSurf.width(src);
operator (gxSurf src).height = gxSurf.height(src);
operator (gxSurf src).depth = gxSurf.depth(src);

// member
operator (gxSurf src).getPixel(int x, int y) = gxSurf.getPixel(src, x, y);

// indexer
operator (gxSurf src)[int x, int y] = gxSurf.getPixel(src, x, y);

// usage
gxSurf src = gxSurf("1.jpg");
int width = src.width;              // => operator (gxSurf src).width
int height = src.height;            // => operator (gxSurf src).height

int color1 = src.getPixel(10, 10);  // => operator (gxSurf src).getPixel(int x, int y)
int color2 = src[10][10];           // => operator (gxSurf src)[int x, int y]
```


## Functions

**Syntax:**

```
('static' | 'const')* type name '(' args? ')' (('=' expr) | ('{' stmt* '}'))? ';'
```

**Example:**

```
float64 sum(float64 data[]) {
	result = 0;
	for (int i = 0; i < data.length; i += 1) {
		result += data[i];
	}
	return result;
}
```

- Functions are assigned and passed to functions as delegates,
meaning that there is an extra parameter pushed to the stack,
a pointer to the callee stack to access captured variables,
or the (this/self/first arg) object in case of instance methods.

- When generating functions, an extra instruction has to be generated
before the implementation, this way any function can be passed as delegate.
This firs instruction should be `set.b32 0`, pop the closure parameter
from the stack replacing it with the return address placed by call.


**Example:**

```
float64 sum(float64 data[]) {
	double sum = 0;
	foreach(data, inline void(float64 value) {
		// sum is a closure variable.
		sum += value;
	})
	return sum;
}
```


### Functions with special names

If a function name is a type name, by convention it is considered to be a constructor for that type,
so it should return an instance of the type represented by the function name.

Another special function name is used for the foreach iterator to advance to the next element.
The iterator function should take the form: `bool next(ref iterator it[, out element current]);`


### Functions without bodies
Functions that are not implemented, will be converted to function references.

These kind of functions must be initialized, or implemented (re-declared as normal function).

#### Function references

Function references can be initialized to point to a function. They can be used also as function parameters.

**Example:**

```
// referencing the sin function
double reference(double value) = Math.sin;

// invoke the function.
double x = sin();

// compare is a function reference.
void sort(int values[], int compare(int a, int b)) {
	...
}
```


#### Forward Functions

**Example:**

```
// forward declaration of function.
double forward();

// invoke the function.
double x = forward();

// implementaton of forward declared function.
double forward() {
	return Math.pi;
}
```


#### Forward member Functions
If a function inside a record is declared as a forward function, it means that it must be:
	- implemented in a record that extends this record.
	- assigned when an instance is created.

This allows the language to create "abstract" like records, with abstract methods.

[**Example:**](../Tests/lang.function.ci#L35)

```
struct Iterator: object {
	// abstract iterator method
	bool next(Iterator it);
}

struct RangeIterator: Iterator {
	int value = -1;
	const int max;

	// implement the abstract method
	bool next(RangeIterator it) {
		if (it.value < it.max) {
			it.value += 1;
			return true;
		}
		return false;
	}
}

RangeIterator rit = { max: 30 };
for ( ; rit.next(); ) {
	print(rit.value);
}
```


## Variables

**Syntax:**

```
('static' | 'const')* type name ('['expr?']')? ('=' expr)? ';'
```

A variable is a named property which value may change at runtime.

### Const variables

Variables marked with the `const` attribute may be assigned only at initialization.

### Static variables

Variables marked with the `static` attribute will point to the same global memory.

Initialization of all static variables are executed when the main function is executed.

### Variable Initialization

### Record initialization

```
// call the initializer function
Complex x = Complex(1, 2);

// TODO: literal initialization
Complex x = {re: 1, im: 2};

// TODO: literal initialization with type
Model x = Sphere {x:0, y:0, z:0, radius: 20};
```

### Array initialization

```
// initialize the first 3 elements with the given values
// the rest will be initalized with 0 (default type initializer)
int a[100] = {1, 2, 3};

// initialize all elements with the value 4
int a[100] = {*: 4};

// initialize all elements with null
// then override some of them
string xmlEscape[255] = {
	*: null,
	'"': "&quot;",
	'\'': "&apos;";
	'<': "&lt;",
	'>': "&gt;",
	'&': "&amp;";
};

//? initialize all elements with a function
char chrStrings[256][2] = {{
	for (int i = 0; i < 256; i += 1) {
		chrStrings[i] = {i, 0};
	}
}};
inline string(char chr) = emit(string, (chrStrings[chr]));
```

### Default type initializer

Builtin types should have default initializer.
pointer, variant, typename, function instances must be explicitly initialized.
All enumerated types should have no default initializer, must be explicitly initialized.

```
int a;       // ok, a is initialized with default type initializer.
typename b;  // error: b is not initialized.
```

### Default field initializer

All constant fields of a record must be initialized when creating an instance.

```
Complex x;   // error: variable x is not initialized.

Complex x = {};   // error: variable x has fields not initialized.

Complex x = {re: 2};   // ok, im initialized with default field initializer.

Complex x = {re: 2; im: -1;};   // ok, all fields are initialized.
```
