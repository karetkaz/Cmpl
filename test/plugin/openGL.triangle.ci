inline fpsCounter = !true;
static int maxx = 500;
static int maxy = 500;

struct vertex:1 {
	float32 x;
	float32 y;
	int32 c;
}

inline vertex(float32 x, float32 y, int32 c) = vertex(emit(int32(c), float32(y), float32(x)));

static vertex vertices[3];

vertices[0] = vertex( 0, +1, 0xff0000);
vertices[1] = vertex(-1, -1, 0x00ff00);
vertices[2] = vertex(+1, -1, 0x0000ff);

void mouse(int btn, int x, int y) {
	// trace("mouse");
	uint32 idx = btn - 1;
	if (idx >= vertices.length) {
		return;
	}

	vertex v& = vertices[idx];
	v.x = -1.f + x / (.5f * maxx);
	v.y = 1.f - y / (.5f * maxy);
	glut.postRedisplay();
	//trace("vertex", v);
}

void keyboard(int key) {
	trace("keyboard", key);
	if (key == 27) {
		glut.exit();
	}
	if (key == 'f') {
		glut.fullScreen();
	}
	if (key == ' ') {
		char value[65536];
		// gl.Vendor
		gl.getString(gl.Vendor, value);
		trace("gl.Vendor", value);

		// gl.Renderer
		gl.getString(gl.Renderer, value);
		trace("gl.Renderer", value);

		// gl.Version
		gl.getString(gl.Version, value);
		trace("gl.Version", value);

		// gl.ShLVersion
		gl.getString(gl.ShLVersion, value);
		trace("gl.ShLVersion", value);

		// gl.Extensions
		gl.getString(gl.Extensions, value);
		trace("gl.Extensions", value);
	}
}

void reshape(int x, int y) {
	//trace("reshape");
	gl.viewport(0, 0, x, y);
	glut.postRedisplay();
	maxx = x;
	maxy = y;
}

void display() {
	//trace("display");

	gl.clear(gl.ColorBuffer);

	gl.begin(gl.Triangles);
	for (int i = 0; i < vertices.length; i += 1) {
		vertex v = vertices[i];
		float r = float32(v.c >> 16 & 0xff) / 255f;
		float g = float32(v.c >> 8 & 0xff) / 255f;
		float b = float32(v.c >> 0 & 0xff) / 255f;
		gl.color(r, g, b, 1);
		gl.vertex(v.x, v.y, 0, 1);
	}
	gl.end();
	glut.swapBuffers();

	static if (fpsCounter) {
		static int clk = 0;
		static int fps = 0;

		fps += 1;
		int now = System.time();
		if (clk != now) {
			debug("fps", fps);
			clk = now;
			fps = 0;
		}
		glut.postRedisplay();
	}
}

glut.onMouse(mouse);
glut.onMotion(mouse);
glut.onKeyboard(keyboard);

glut.onReshape(reshape);
glut.onDisplay(display);

glut.initDisplayMode(glut.Double|glut.Rgb);

// this wont let wmExec to exit.
glut.mainLoop(800, 600, "Cmpl - OpenGL");
