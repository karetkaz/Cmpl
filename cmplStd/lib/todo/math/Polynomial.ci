// Implementation of a Polynomial

inline "/cmplStd/lib/system/Stream.ci";

struct Polynomial/*(typename scalar): TextWriter.Writeable*/: object {
	inline scalar = float64;
	scalar elements[*];
	const int capacity;

	enum: scalar {
		c_zero: 0;
		c_unit: 1;
	}

	/* TODO: implement enums
	enum: Polynomial {
		zero: {
			elements: { c_zero };
			capacity: 1;
		}
	} */

	// construct a new instance
	static Polynomial make(int capacity) {
		scalar elements[*] = pointer.alloc(null, capacity * sizeof(scalar));
		for (int i = 0; i < capacity; i += 1) {
			elements[i] = c_zero;
		}
		return {
			elements: elements;
			capacity: capacity;
		};
	}

	// clone an existing instance
	static Polynomial copy(const Polynomial instance&) {
		scalar elements[*] = pointer.alloc(null, instance.capacity * sizeof(scalar));
		for (int i = 0; i < instance.capacity; i += 1) {
			elements[i] = instance.elements[i];
		}
		return {
			elements: elements;
			capacity: capacity;
		};
	}

	static int degree(Polynomial this&) {
		for (int i = this.capacity - 1; i > 0; i -= 1) {
			if (this.elements[i] != c_zero) {
				return i;
			}
		}
		return 0;
	}

	static scalar evaluate(const Polynomial this&, scalar x) {
		int deg = this.degree();
		scalar result = this.elements[deg];
		for (int i = deg - 1; i >= 0; i -= 1) {
			result = result * x + this.elements[i];
		}
		return result;
	}

	static scalar get(const Polynomial this&, int index) {
		assert(index >= 0, "negative index");
		if (index >= this.capacity) {
			return c_zero;
		}
		return this.elements[index];
	}

	static void set(Polynomial this&, int index, scalar value) {
		assert(index >= 0, "negative index");
		assert(index < this.capacity, "invalid index");
		this.elements[index] = value;
	}


	static Polynomial neg(const Polynomial a&) {
		Polynomial result = make(a.degree());
		for (int i = 0; i < result.capacity; i += 1) {
			result.elements[i] = -a.get(i);
		}
		return result;
	}

	static Polynomial add(const Polynomial a&, const Polynomial b&) {
		Polynomial result = make(Math.max(a.degree(), b.degree()));
		for (int i = 0; i < result.capacity; i += 1) {
			result.elements[i] = a.get(i) + b.get(i);
		}
		return result;
	}

	static Polynomial sub(const Polynomial a&, const Polynomial b&) {
		Polynomial result = make(Math.max(a.degree(), b.degree()));
		for (int i = 0; i < result.capacity; i += 1) {
			result.elements[i] = a.get(i) - b.get(i);
		}
		return result;
	}

	static Polynomial mul(const Polynomial a&, const Polynomial b&) {
		int aDeg = a.degree();
		int bDeg = b.degree();
		Polynomial result = make(aDeg + bDeg);
		for (int i = 0; i <= aDeg; i += 1) {
			for (int j = 0; j <= bDeg; j += 1) {
				result.elements[i + j] += a.get(i) * b.get(i);
			}
		}
		return result;
	}

	static void write(const Polynomial this&, TextWriter writer) {
		bool first = true;
		for (int i = this.degree(); i >= 0; i -= 1) {
			scalar value = this.elements[i];
			if (value == c_zero && !(first && i == 0)) {
				continue;
			}

			if (value < c_zero) {
				value = -value;
				if (!first) {
					// binary operator
					writer.write(" - ");
				}
				else {
					// unary operator
					writer.write("-");
				}
			}
			else if (!first) {
				// binary operator
				writer.write(" + ");
			}

			if (value != c_unit || i == 0) {
				writer.write(value);
			}

			if (i > 0) {
				writer.write("x");
				if (i > 1) {
					writer.write("^");
					writer.write(i);
				}
			}
			first = false;
		}
	}
}

inline Polynomial(int capacity) = Polynomial.make(capacity);

inline Polynomial(const Polynomial instance&) = Polynomial.copy(instance);

// destroy the instance
void void(Polynomial value&) {
	pointer.alloc(value.elements, 0);
	value.elements = null;
}

Polynomial p = Polynomial.make(10);
// p.set(0, 0);
// p.set(0, 1);
/* TODO: implement operator overloading
inline [](Polynomial &this, int index) = Polynomial.get(this, index);
inline ()(Polynomial &this, scalar x) = Polynomial.evaluate(this, x);

inline -(const Polynomial &a) = Polynomial.neg(a);
inline +(const Polynomial &a, const Polynomial &b) = Polynomial.add(a, b);
inline -(const Polynomial &a, const Polynomial &b) = Polynomial.sub(a, b);
inline *(const Polynomial &a, const Polynomial &b) = Polynomial.mul(a, b);
// */
