// micro ui

struct View: object {
	const char text[*];
	bool enabled = true;
	bool selectable = true;
	const variant data = null;

	bool onClick(View this) = null; /*TODO: {
		//trace("onClick", this);
		return true;
	}*/
}

struct Button: View {
	/*TODO: bool onClick(Button this) {
		trace("onClick", this);
		return true;
	}*/
}

struct Switch: View {
	const char on[*] = "on";
	const char off[*] = "off";

	bool checked = false;
}

struct Slider: View {
	int value = 0;
	const int minimum = 0;
	const int maximum = -1u >> 1;
	const int divisor = 1;

	/* FIXME: do the override, use this method as default initializer
	// TODO: override or overload: bool onClick(Slider this) { ??? }
	bool onClick(View this) {
		View.onClick(this);   // call super
		trace("onClick", this);
		// FIXME: enable lookup: value.value = 0;
		return this.enabled;
	};
	// */
}

struct MouseState {
	int32 clicks;   // click count
	int64 time;     // old clock
	int32 btn;      // old button
	int32 x;        // old x coord
	int32 y;        // old y coord
}

struct Layout {
	gxSurf offs;
	gxSurf font;
	MouseState mouse;
	View items[200] = {};
	gxRect rects[200] = {};
	int reset[200] = {};
	int selectedIndex = -1;
	int count = 0;
}

int val(Slider slider&) {
	if (slider.value < slider.minimum) {
		slider.value = slider.minimum;
	}
	if (slider.value > slider.maximum) {
		slider.value = slider.maximum;
	}
	return slider.value;
}

double valFlt(Slider slider&) {
	return slider.val() / double(slider.divisor);
}

Layout Layout(gxSurf offs, gxSurf font, View items...) {
	Layout layout;/* FIXME:BUG: = {
		selectedIndex: -1;
		// rect: {x: 0, y: 0, w: 0, h: 0};
	};*/
	layout.offs = offs;
	layout.font = font;
	layout.selectedIndex = -1;
	for (int i = 0; i < items.length; i += 1) {
		if (Slider slider = items[i].as(Slider)) {
			layout.reset[i] = slider.value;
		}
		layout.items[i] = items[i];
	}
	layout.count = items.length;
	return layout;
}

void draw(Layout layout&, int x, int y) {
	int posAll = 0;
	char textAll[1024];
	FormatFlags flagsInt = { padLen: 4 };
	FormatFlags flagsFlt = { padLen: 4, precision: 2 };
	gxRect rectAll = { x:x, y:y, w:0, h:0 };

	for (int i = 0; i < layout.count; i += 1) {
		int pos = 0;
		char text[256];
		gxRect rect& = layout.rects[i];
		if (i == layout.selectedIndex) {
			pos = text.append(pos, "* ");
		} else {
			pos = text.append(pos, " ");
		}
		View view = layout.items[i];
		pos = text.append(pos, view.text);
		if (Slider item = view.as(Slider)) {
			pos = text.append(pos, ": ");
			if (item.divisor != 1) {
				pos = append(text, pos, item.valFlt(), flagsFlt);
			} else {
				pos = text.append(pos, item.val(), flagsInt);
			}
		}
		else if (Switch item = view.as(Switch)) {
			pos = text.append(pos, ": ");
			if (item.checked) {
				pos = text.append(pos, item.on);
			} else {
				pos = text.append(pos, item.off);
			}
		}
		else if (Button item = view.as(Button)) {
		}
		else {
			abort("item is not a known ui element", view);
		}
		posAll = textAll.append(posAll, text);
		posAll = textAll.append(posAll, "\n");
		layout.font.clipText(rect, text);
		rect.x = x;
		rect.y = y;
		y += rect.h;
	}
	layout.font.clipText(rectAll, textAll);
	layout.offs.fill(rectAll, 5, 0x000000);
	layout.offs.drawText(rectAll.x, rectAll.y, layout.font, textAll, 0xffffff);
}

inline selected(Layout layout&) = layout.selectedIndex < 0 ? null : pointer(layout.items[layout.selectedIndex]);
inline selected(Layout layout&, typename type) = layout.selectedIndex < 0 ? null : layout.items[layout.selectedIndex].as(type);
inline isSelected(Layout layout&, View item) = item == layout.selected();

bool uiEvent(Layout layout&, int action, int button, int x, int y) {
static int click(MouseState old&, int btn, int x, int y) {
	if ((btn != 0 && btn != old.btn) || x != old.x || y != old.y) {
		// reset values
		old.time = System.millis();
		old.btn = btn;
		old.clicks = 1;
		old.x = x;
		old.y = y;
		return 0;
	}
	if (btn == 0) {
		// release button
		// old.time = keep value?;
		// old.btn = keep value?;
		// old.cnt = keep value?;
		return old.clicks;
	}

	int64 now = System.millis();
	if ((now - old.time) / 1000. > .5) {
		// reset counter
		old.clicks = 0;
	}
	old.time = now;
	old.btn = btn;
	old.clicks += 1;
	return old.clicks;
}

// TODO: void measure(Layout layout&) { }
// TODO: void layout(Layout layout&) { }

bool click(Layout layout&, int index) {
	if (index < 0) {
		return false;
	}
	View view = layout.items[index];
	if (Slider item = view.as(Slider)) {
		if (!item.enabled) {
			return false;
		}
		if (item.selectable) {
			layout.selectedIndex = index;
		}
		if (layout.mouse.clicks > 1) {
			item.value = layout.reset[index];
		}
		if (view.onClick != null) {
			// FIXME: use: view.onClick(item)
			return item.onClick(item);
		}
		return true;
	}
	if (Switch item = view.as(Switch)) {
		if (!item.enabled) {
			return false;
		}
		int previousIndex = index;
		if (item.selectable) {
			previousIndex = layout.selectedIndex;
			layout.selectedIndex = index;
		}
		if (previousIndex == index) {
			// change only if already selected
			item.checked = !item.checked;
		}
		if (item.onClick != null) {
			return item.onClick(item);
		}
		return true;
	}
	if (Button item = view.as(Button)) {
		if (!item.enabled) {
			return false;
		}
		if (item.selectable) {
			layout.selectedIndex = index;
		}
		if (item.onClick != null) {
			return item.onClick(item);
		}
		return true;
	}

	abort("item is not a known ui element", view);
	return false;
}
bool click(Layout layout&, int x, int y) {
	bool test(const gxRect rect&, int x, int y) {
		if (x < rect.x || x >= rect.x + rect.w) {
			return false;
		}
		if (y < rect.y || y >= rect.y + rect.h) {
			return false;
		}
		return true;
	}

	for (int i = 0; i < layout.count; i += 1) {
		if (!layout.rects[i].test(x, y)) {
			continue;
		}
		return click(layout, i);
	}
	// click is outside any control
	return false;
}

bool update(Layout layout&, int delta) {
	if (layout.selectedIndex < 0) {
		return false;
	}
	View selectedItem = layout.items[layout.selectedIndex];
	if (Slider slider = selectedItem.as(Slider)) {
		slider.value += delta;
		return true;
	}
	if (Switch switch = selectedItem.as(Switch)) {
		switch.checked = delta < 0;
		return true;
	}
	return false;
}

bool reset(Layout layout&, int index) {
	if (index < 0) {
		for (int i = 0; i < layout.count; i += 1) {
			layout.reset(i);
		}
		return true;
	}
	if (Slider item = layout.items[index].as(Slider)) {
		item.value = layout.reset[index];
		return true;
	}
	return false;
}

bool select(Layout layout&, bool prev) {
	for (int i = 1; i < layout.count; i += 1) {
		int index = layout.selectedIndex + (prev ? -i : i);
		if (index >= layout.count) {
			index -= layout.count;
		}
		else if (index < 0) {
			index += layout.count;
		}
		View view = layout.items[index];
		if (Button item = view.as(Button)) {
			if (item.enabled && item.selectable) {
				layout.selectedIndex = index;
				return true;
			}
		}
		else if (Switch item = view.as(Switch)) {
			if (item.enabled && item.selectable) {
				layout.selectedIndex = index;
				return true;
			}
		}
		else if (Slider item = view.as(Slider)) {
			if (item.enabled && item.selectable) {
				layout.selectedIndex = index;
				return true;
			}
		}
	}
	return false;
}

	bool isCtrl = (y & Gui.KEY_MASK_CONTROL) != 0;
	bool isShift = (y & Gui.KEY_MASK_SHIFT) != 0;

	if (action == Gui.FINGER_MOTION) {
		action = Gui.MOUSE_MOTION;
	}

	if (action == Gui.WINDOW_INIT) {
		layout.selectedIndex = -1;
		layout.select(false);
		return true;
	}
	else if (action == Gui.KEY_PRESS) {
		if (button == 8) {	// backspace
			if (layout.reset(isShift ? -1 : layout.selectedIndex)) {
				return true;
			}
		}
		if (button == ' ') {
			if (layout.click(layout.selectedIndex)) {
				return true;
			}
		}
		if (button == '-' || button == '_') {	// down
			int delta = isShift ? 10 : 1;
			if (layout.update(-delta)) {
				return true;
			}
		}
		if (button == '+' || button == '=') {	// up
			int delta = isShift ? 10 : 1;
			if (layout.update(+delta)) {
				return true;
			}
		}
		if (button == '\t') {
			if (layout.select(isShift)) {
				return true;
			}
		}
	}
	else if (action == Gui.MOUSE_PRESS) {
		layout.mouse.click(button, x, y);
		if (button == 1) {
			if (layout.click(x, y)) {
				return true;
			}
		}
	}
	else if (action == Gui.MOUSE_RELEASE) {
		layout.mouse.click(0, x, y);
	}
	else if (action == Gui.MOUSE_MOTION) {
		int dy = layout.mouse.y - y;
		layout.mouse.click(-1, x, y);
		if (button == 1) {
			if (layout.update(dy)) {
				return true;
			}
		}
	}
	return false;
}
