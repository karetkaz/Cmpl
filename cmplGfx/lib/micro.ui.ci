// micro ui, TODO: add comments

struct Style {
	const int align;
	const int border = 3;
	const int padding = 8;

	const Image font = Gui.font;
	const FormatFlags fmtInt;
	const FormatFlags fmtFlt;

	int backgroundColor = 0x1a1a1a;
	int hoveredColor = 0x2a2a2a;
	int focusedColor = 0x3b4754;
	int valueColor = 0x6897bb;
	int textColor = 0xdadada;

	enum: int32 {
		center: 0;

		left: 1;
		right: 2;
		vertical: left + right;

		top: 4;
		bottom: 8;
		horizontal: top + bottom;

		fill: vertical + horizontal;
	}

	enum: int32 {
		hovered: 1;
		pressed: 2;
		focused: 4;
	}

	static void measure(const Style this&, Rect rect&, const char text[*], int extraChars) {
		this.font.clipText(&rect, text);
		if (text != null && rect.w != 0) {
			rect.w += extraChars * rect.w / length(text);
		}
		rect.w += 2 * this.padding;
		rect.h += 2 * this.padding;
	}

	/// Align one rect containing text relative to its container
	static void align(Rect parent, Rect rect&, int align) {
		if (rect.w > parent.w) {
			rect.w = parent.w;
		}
		if (rect.h > parent.h) {
			rect.h = parent.h;
		}

		int vertical = align & Style.vertical;
		if (vertical == center) {
			rect.x = parent.x + (parent.w - rect.w) / 2;
		}
		else if (vertical == right) {
			rect.x = parent.x + (parent.w - rect.w);
		}
		else if (vertical == left) {
			rect.x = parent.x;
		}
		else { // fit
			rect.x = parent.x;
			rect.w = parent.w;
		}

		int horizontal = align & Style.horizontal;
		if (horizontal == center) {
			rect.y = parent.y + (parent.h - rect.h) / 2;
		}
		else if (horizontal == bottom) {
			rect.y = parent.y + (parent.h - rect.h);
		}
		else if (horizontal == top) {
			rect.y = parent.y;
		}
		else { // fit
			rect.y = parent.y;
			rect.h = parent.h;
		}
	}

	static void align(const Style this&, Rect parent, Rect rect&, const char value[*], int padding, int align) {
		parent.x += padding;
		parent.y += padding;
		parent.w -= 2 * padding;
		parent.h -= 2 * padding;
		this.font.clipText(&rect, value);
		align(parent, &rect, align);
	}

	static void align(const Style this&, Rect parent, Rect rect&, const char value[*], int align) {
		return align(this, parent, &rect, value, this.padding, align);
	}

	static void align(const Style this&, Rect parent, Rect rect&, const char value[*]) {
		return align(this, parent, &rect, value, this.padding, this.align);
	}
}
struct KeyEvent {
	const bool release;
	const bool isShift;
	const bool isCtrl;
	const bool isAlt;
	const int repeat;
	const int key;
}
struct MouseEvent {
	const bool release;
	const bool isShift;
	const bool isCtrl;
	const bool isAlt;
	const int clicks;
	const int button;
	const int dx = 0;
	const int dy = 0;
	const int x;
	const int y;

	static bool test(const MouseEvent event&, const Rect rect&) {
		if (event.x < rect.x || event.x >= rect.x + rect.w) {
			return false;
		}
		if (event.y < rect.y || event.y >= rect.y + rect.h) {
			return false;
		}
		return true;
	}

	bool isMotion(const MouseEvent event&) {
		return event.dx || event.dy;
	}

	bool isPress(const MouseEvent event&) {
		return !(event.release || event.dx || event.dy);
	}

	bool isLongPress(const MouseEvent event&) {
		return event.release && event.clicks < 0;
	}

	bool isPress(const MouseEvent event&, int button) {
		if (event.release || event.dx || event.dy) {
			return false;
		}
		return event.button == button;
	}
}

struct View: object {
	/// fixed or minimum width of the view
	const int width = -5;

	/// fixed or minimum height of the view
	const int height = -5;

	/// visibility flags (the view will be visible if Widget.visibleGroup matches at least one common bit set)
	const int visible = -1;

	/// the view accepts any user input(event) or not
	const bool enabled = true;

	/// the view can be active(focused) or not
	const bool focusable = true;

	/// user attached data
	variant data = null;

	bool onClick(View this) {
		return true;
	}

	bool onReset(View this) {
		return false;
	}

	void onCreate(View this) {
	}

	void onDestroy(View this) {
	}

	bool onKeyEvent(View this, const KeyEvent event&) {
		if (!this.enabled) {
			// disabled view does not consume events
			return false;
		}
		if (event.release) {
			return false;
		}
		if (event.key == 8 && !event.isShift) {
			return this.onReset(this);
		}
		if (event.key == ' ') {
			return this.onClick(this);
		}
		return false;
	}

	bool onMouseEvent(View this, const Rect rect&, const MouseEvent event&) {
		/* press, release or motion
		if (event.dx || event.dy) {
			return onMotion(event);
		}
		if (event.release) {
			return onRelease(event);
		}
		return onPress(event);*/
		if (!this.enabled) {
			// disabled view does not consume events
			return false;
		}
		if (!event.release) {
			// press or motion events are not consumed
			return false;
		}

		if (event.clicks < 0) {
			// long press, not released
			return this.onReset(this);
		}
		if (event.clicks > 1) {
			// multi click to reset
			if (this.onReset(this)) {
				// do not click with press and hold
				return true;
			}
		}
		return this.onClick(this);
	}

	void onMeasure(View this, Rect rect&, const Style style&) {
		if (this.width >= 0) {
			rect.w = this.width;
		}
		else if (Math.abs(rect.w) < -this.width) {
			rect.w = -this.width;
		}
		if (this.height >= 0) {
			rect.h = this.height;
		}
		else if (Math.abs(rect.h) < -this.height) {
			rect.h = -this.height;
		}
	}

	void onDraw(View this, Image offs, Rect rect, const Style style&);
	static void onDraw(View this, Image offs, Rect rect, const Style style&, int state) {
		// TODO: dispatch state to all child views
		if (state & Style.focused) {
			offs.fillRect(rect, style.focusedColor);
		}
		else if (state & Style.hovered) {
			offs.fillRect(rect, style.hoveredColor);
		}
		this.onDraw(this, offs, rect, style);
		if (state & Style.pressed) {
			offs.drawRect(rect, style.valueColor);
		}
	}
	static void onDraw(View this, Image offs, Rect rect, const Style style&, const char text[*], const char value[*]) {
		if (text == null) {
			style.align(rect, &rect, value, Style.center);
			offs.drawText(rect, style.font, value, style.valueColor);
			return;
		}

		Rect valueRoi = rect;
		style.align(valueRoi, &valueRoi, value, Style.right);
		rect.w -= valueRoi.w + style.padding;
		style.align(style, rect, &rect, text);
		offs.drawText(valueRoi, style.font, value, style.valueColor);
		offs.drawText(rect, style.font, text, style.textColor);
	}
}

struct Layout: View {
	/* TODO: vertical and horizontal scroll offset
	int xOffset;
	int yOffset;
	*/

	/** Vertical, horizontal or other orientation of the layout
	 * zero represents a stack(Qt) or frame(Android) layout (all child elements are placed on top of each other)
	 * negative value represents a column layout, specifying the number of columns to be used
	 * positive value represents a row layout, specifying the number of rows to be used
	 */
	const int orientation;

	/// Alignment of each child in the layout
	const int alignment;

	const int itemCount;
	View items[*] = null;
	Rect rects[*] = null;

	int hoveredIndex = -1;
	int pressedIndex = -1;
	int focusedIndex = -1;

	static bool clearState(Layout this, int mode) {
		bool result = false;
		if (mode & Style.hovered && this.hoveredIndex >= 0) {
			View view = this.items[this.hoveredIndex];
			if (Layout layout = view.as(Layout)) {
				clearState(layout, mode);
			}
			this.hoveredIndex = -1;
			result = true;
		}
		if (mode & Style.pressed && this.pressedIndex >= 0) {
			View view = this.items[this.pressedIndex];
			if (Layout layout = view.as(Layout)) {
				clearState(layout, mode);
			}
			this.pressedIndex = -1;
			result = true;
		}
		if (mode & Style.focused && this.focusedIndex >= 0) {
			View view = this.items[this.focusedIndex];
			if (Layout layout = view.as(Layout)) {
				clearState(layout, mode);
			}
			this.focusedIndex = -1;
			result = true;
		}
		return result;
	}
	static int findView(Layout this, int x, int y) {
		for (int i = 0; i < this.itemCount; i += 1) {
			Rect rect& = this.rects[i];
			if (y >= rect.y && y < rect.y + rect.h) {
				if (x >= rect.x && x < rect.x + rect.w) {
					return i;
				}
			}
		}
		return -1;
	}

	bool onClick(Layout this) {
		error("onClick", this);
		return false;
	}

	bool onReset(Layout this) {
		bool result = false;
		for (int i = 0; i < this.itemCount; i += 1) {
			View view = this.items[i];
			if (view.onReset(view)) {
				result = true;
			}
		}
		return result;
	}

	void onCreate(Layout this) {
		for (int i = 0; i < this.itemCount; i += 1) {
			View view = this.items[i];
			view.onCreate(view);
		}
	}

	void onDestroy(Layout this) {
		// destroy views and free up allocations
		for (int i = 0; i < this.itemCount; i += 1) {
			View view = this.items[i];
			view.onDestroy(view);
			view.destroy();
		}
		pointer.alloc(this.items, 0);
		pointer.alloc(this.rects, 0);
	}

	bool onKeyEvent(Layout this, const KeyEvent event&) {
		int idx = this.focusedIndex;
		if (idx < 0) {
			return false;
		}

		View view = this.items[idx];
		return view.onKeyEvent(view, event);
	}

	bool onMouseEvent(Layout this, const Rect rect&, const MouseEvent event&) {
		bool result = false;
		int active = this.pressedIndex;
		if (!(event.dx || event.dy || event.release) || !event.button) {
			active = findView(this, event.x, event.y);
		}

		if (active < 0) {
			// not an active view
			int states = Style.hovered;
			if (event.release) {
				states |= Style.pressed;
			}
			return this.clearState(states);
		}

		View view = this.items[active];
		if (!view.enabled) {
			// not an enabled view
			int states = Style.hovered;
			if (event.release) {
				states |= Style.pressed;
			}
			return this.clearState(states);
		}

		if (this.pressedIndex != active && event.isPress(1)) {
			// pressed a different view, update pressed view
			this.clearState(Style.pressed);
			this.pressedIndex = active;
			result = true;
		}

		bool outside = !event.test(this.rects[active]);
		if (this.hoveredIndex != active || outside) {
			// update current view as hovered
			if (this.clearState(Style.hovered)) {
				result = true;
			}
			if (!outside) {
				this.hoveredIndex = active;
				result = true;
			}
		}

		if (event.release && outside) {
			// return if released outside the pressed view
			return this.clearState(Style.pressed|Style.hovered);
		}

		if (view.onMouseEvent(view, this.rects[active], event)) {
			// event not processed by the view
			result = true;
		}

		if (!event.release) {
			return result;
		}

		bool focusable = view.focusable;
		if (outside) {
			focusable = false;
		}
		else if (event.clicks < 0) {
			// do not focus on long press
			this.clearState(Style.hovered);
			focusable = false;
		}
		else if (this.focusedIndex == active) {
			// already in focus, do not focus
			focusable = false;
		}
		else if (Layout layout = view.as(Layout)) {
			if (layout.focusedIndex == -1) {
				// child is a layout, with no focused item
				focusable = false;
			}
		}

		if (focusable) {
			this.clearState(Style.focused);
			this.focusedIndex = active;
			result = true;
		}
		if (this.clearState(Style.pressed)) {
			return true;
		}
		return result;
	}

	void onMeasure(Layout this, Rect rect&, const Style style&) {
		int maxWidth = 0;
		int sumWidth = 0;
		int maxHeight = 0;
		int sumHeight = 0;
		for (int i = 0; i < this.itemCount; i += 1) {
			View view = this.items[i];
			Rect viewRect& = this.rects[i];
			viewRect.w = viewRect.h = 0;

			view.onMeasure(view, &viewRect, style);
			int width = Math.abs(viewRect.w);
			int height = Math.abs(viewRect.h);
			if (maxWidth < width) {
				maxWidth = width;
			}
			if (maxHeight < height) {
				maxHeight = height;
			}
			sumWidth += width;
			sumHeight += height;
		}

		if (this.orientation > 0) {
			// using negative sum because hidden views are included
			rect.w = -sumWidth;
		} else {
			rect.w = maxWidth;
		}
		if (this.orientation < 0) {
			// using negative sum because hidden views are included
			rect.h = -sumHeight;
		} else {
			rect.h = maxHeight;
		}
		View.onMeasure(this, &rect, style);
	}

	void onLayout(Layout this, Rect rect&);
	static void frameLayout(Layout this, Rect rect&) {
		Rect r = rect;
		r.w = Math.abs(r.w);
		r.h = Math.abs(r.h);
		int align = this.alignment;
		for (int i = 0; i < this.itemCount; i += 1) {
			View view& = this.items[i];
			Rect viewRect& = this.rects[i];
			Style.align(r, &viewRect, align);
			if (Layout layout = view.as(Layout)) {
				layout.onLayout(layout, &viewRect);
			}
			if (rect.w < viewRect.w) {
				rect.w = viewRect.w;
			}
			if (rect.h < viewRect.h) {
				rect.h = viewRect.h;
			}
		}
	}
	static void verticalLayout(Layout this, Rect rect&) {
		int gravity = Style.horizontal + Style.left;
		int align = this.alignment & Style.vertical;
		int gap = 0;
		int width = 0;
		int height = 0;
		Rect r = rect;
		r.w = Math.abs(r.w);
		r.h = Math.abs(r.h);
		for (int i = 0; i < this.itemCount; i += 1) {
			Rect viewRect& = this.rects[i];
			width += Math.abs(viewRect.w);
		}
		if (rect.w > width) {
			gap = r.w - width;
			if (align != Style.right) {
				if (align == Style.vertical) {
					gap /= this.itemCount;
				}
				else if (this.itemCount > 1) {
					gap /= this.itemCount - 1;
				}
			}
		}
		for (int i = 0; i < this.itemCount; i += 1) {
			View view& = this.items[i];
			Rect viewRect& = this.rects[i];
			Style.align(r, &viewRect, gravity);
			if (Layout layout = view.as(Layout)) {
				layout.onLayout(layout, &viewRect);
			}
			if (viewRect.w > 0 && viewRect.h > 0) {
				if (align == Style.vertical) {
					r.x += gap + viewRect.w;
					viewRect.w += gap;
					if (Layout layout = view.as(Layout)) {
						layout.onLayout(layout, &viewRect);
					}
				}
				else if (align == Style.center) {
					r.x += gap + viewRect.w;
				}
				else if (align == Style.right) {
					r.x += viewRect.w;
					viewRect.x += gap;
					if (Layout layout = view.as(Layout)) {
						layout.onLayout(layout, &viewRect);
					}
				}
				else if (align == Style.left) {
					r.x += viewRect.w;
				}
			}
		}
		if (rect.w < width) {
			rect.w = width;
		}
		if (rect.h < height) {
			rect.h = height;
		}
	}
	static void horizontalLayout(Layout this, Rect rect&) {
		int gravity = Style.vertical + Style.top;
		int align = this.alignment & Style.horizontal;
		int gap = 0;
		int width = 0;
		int height = 0;
		Rect r = rect;
		r.w = Math.abs(r.w);
		r.h = Math.abs(r.h);
		for (int i = 0; i < this.itemCount; i += 1) {
			Rect viewRect& = this.rects[i];
			height += Math.abs(viewRect.h);
		}
		if (rect.h > height) {
			gap = r.h - height;
			if (align != Style.bottom) {
				if (align == Style.horizontal) {
					gap /= this.itemCount;
				}
				else if (this.itemCount > 1) {
					gap /= this.itemCount - 1;
				}
			}
		}
		for (int i = 0; i < this.itemCount; i += 1) {
			View view& = this.items[i];
			Rect viewRect& = this.rects[i];
			Style.align(r, &viewRect, gravity);
			if (Layout layout = view.as(Layout)) {
				layout.onLayout(layout, &viewRect);
			}
			if (viewRect.w > 0 && viewRect.h > 0) {
				if (align == Style.horizontal) {
					r.y += gap + viewRect.h;
					viewRect.h += gap;
					if (Layout layout = view.as(Layout)) {
						layout.onLayout(layout, &viewRect);
					}
				}
				else if (align == Style.center) {
					r.y += gap + viewRect.h;
				}
				else if (align == Style.bottom) {
					r.y += viewRect.h;
					viewRect.y += gap;
					if (Layout layout = view.as(Layout)) {
						layout.onLayout(layout, &viewRect);
					}
				}
				else if (align == Style.top) {
					r.y += viewRect.h;
				}
			}
		}
		if (rect.w < width) {
			rect.w = width;
		}
		if (rect.h < height) {
			rect.h = height;
		}
	}

	void onLayout(Layout this, Rect rect&) {
		if (this.orientation == 0) {
			return frameLayout(this, &rect);
		}
		if (this.orientation == 1) {
			return verticalLayout(this, &rect);
		}
		if (this.orientation == -1) {
			return horizontalLayout(this, &rect);
		}

		return abort("not implemented");
		/* TODO: this case is a grid like layout:
		 * Multi Column Layout with 3 columns
			/--- col0 ---+----col1----+----col2----\
			|   item00   |   item01   |   item02   |
			|   item03   |   item04   |            |
			.    ...     |    ...     |    ...     .
			\------------+------------+------------/
		 * Multi Row Layout with 2 rows
			/--- col0 ---+----col1----+----col2----+--- ... ----\
			|   item00   |   item02   |   item04   |    ...     |
			|   item01   |   item03   |    ...     |    ...     |
			\------------+------------+------------+--- ... ----/
		*/
	}

	void onDraw(Layout this, Image offs, Rect rect, const Style style&) {
		// draw items
		for (int i = 0; i < this.itemCount; i += 1) {
			View view = this.items[i];
			const Rect viewRect& = this.rects[i];

			if (viewRect.w > 0 && viewRect.h > 0) {
				int state = 0;
				if (view.as(Layout) == null) {
					if (i == this.hoveredIndex) {
						state |= Style.hovered;
					}
					if (i == this.pressedIndex) {
						state |= Style.pressed;
					}
					if (i == this.focusedIndex) {
						state |= Style.focused;
					}
				}
				View.onDraw(view, offs, viewRect, style, state);
			}
		}
	}

	static Layout Custom(int rowsCols, int align, View items...) {
		Layout layout = {
			orientation: rowsCols;
			alignment: align;
			itemCount: items.length;
			items: pointer.alloc(null, items.length * sizeof(View));
			rects: pointer.alloc(null, items.length * sizeof(Rect));
		};
		for (int i = 0; i < items.length; i += 1) {
			layout.items[i] = items[i];
		}
		return layout;
	}
	static Layout Row(View items...) {
		return Custom(1, Style.horizontal, ...items);
	}
	static Layout Column(View items...) {
		return Custom(-1, Style.vertical, ...items);
	}
	// FIXME: inline Row(View items...) = Row(1, Style.horizontal, ...items);
}

struct Button: View {
	const char text[*];

	void onMeasure(Button this, Rect rect&, const Style style&) {
		style.measure(&rect, this.text, 0);
		View.onMeasure(this, &rect, style);
	}

	void onDraw(Button this, Image offs, Rect rect, const Style style&) {
		style.align(rect, &rect, this.text);
		offs.drawText(rect, style.font, this.text, style.textColor);
	}
}

struct Switch: View {
	static const char onOff[*][] = {
		"off",
		"on"
	};

	const char text[*];
	const char states[*][] = onOff;

	int selected = 0;
	const int resetValue = 0;//TODO: selected;

	static bool checked(Switch this) {
		return this.selected != 0;
	}

	bool onUpdate(Switch this, int selected) {
		if (selected >= this.states.length) {
			selected = this.states.length - 1;
		}
		else if (selected < 0) {
			selected = 0;
		}

		if (this.selected == selected) {
			return false;
		}

		this.selected = selected;
		return true;
	}

	bool onClick(Switch this) {
		int value = this.selected + 1;
		if (value >= this.states.length) {
			value = 0;
		}
		return this.onUpdate(this, value);
	}

	void onCreate(Switch this) {
		// clamp initial value
		Switch.onUpdate(this, this.selected);
		int resetValue& = pointer(this.resetValue);
		resetValue = this.selected;
	}

	bool onKeyEvent(Switch this, const KeyEvent event&) {
		if (event.release) {
			return View.onKeyEvent(this, event);
		}
		if (event.key == '+' || event.key == '=') {
			int value = this.selected + 1;
			if (value >= this.states.length) {
				value = 0;
			}
			return this.onUpdate(this, value);
		}
		if (event.key == '-' || event.key == '_') {
			int value = this.selected - 1;
			if (value < 0) {
				value = this.states.length - 1;
			}
			return this.onUpdate(this, value);
		}
		return View.onKeyEvent(this, event);
	}

	void onMeasure(Switch this, Rect rect&, const Style style&) {
		int len = length(this.states[0]);
		for (int i = 0; i < this.states.length; i += 1) {
			int max = length(this.states[0]);
			if (len < max) {
				len = max;
			}
		}
		style.measure(&rect, this.text, len);
		View.onMeasure(this, &rect, style);
	}

	void onDraw(Switch this, Image offs, Rect rect, const Style style&) {
		View.onDraw(this, offs, rect, style, this.text, this.states[this.selected]);
	}
}

struct Slider: View {
	const char text[*];

	int value = 0;
	const int minimum = 0;
	const int maximum = -1u >> 1;
	const int divisor = 0;
	const int resetValue = 0; // TODO: resetValue = value;

	static float64 valueF64(Slider this) {
		double value = this.value;
		if (this.divisor != 0) {
			return value / this.divisor;
		}
		return value;
	}

	bool onUpdate(Slider this, int value) {
		if (value > this.maximum) {
			value = this.maximum;
		}
		if (value < this.minimum) {
			value = this.minimum;
		}
		if (this.value == value) {
			return false;
		}
		this.value = value;
		return true;
	}

	bool onReset(Slider this) {
		return Slider.onUpdate(this, this.resetValue);
	}

	void onCreate(Slider this) {
		Slider.onUpdate(this, this.value);// clamp initial value
		int resetValue& = pointer(this.resetValue);
		resetValue = this.value;
	}

	bool onKeyEvent(Slider this, const KeyEvent event&) {
		if (event.release) {
			return View.onKeyEvent(this, event);
		}
		if (event.key == ' ') {
			return this.onReset(this);
		}
		if (event.key == '+' || event.key == '=') {
			return this.onUpdate(this, this.value + (event.isShift ? 1 : 10));
		}
		if (event.key == '-' || event.key == '_') {
			return this.onUpdate(this, this.value - (event.isShift ? 1 : 10));
		}
		return View.onKeyEvent(this, event);
	}

	bool onMouseEvent(Slider this, const Rect rect&, const MouseEvent event&) {
		if (event.button == 0) {
			return false;
		}
		if (event.release && (event.clicks < 0 || event.clicks > 1)) {
			return this.onReset(this);
		}

		if (event.clicks < 1) {
			float value = event.dx;
			if (!event.isShift && value != 0) {
				value *= float(this.maximum - this.minimum) / rect.w;
				if (value > -1 && value < 1) {
					value = value < 0 ? -1 : 1;
				}
			}
			return this.onUpdate(this, this.value + value);
		}
		if (event.release) {
			if (event.y < rect.y + rect.h - rect.h / 3) {
				// clicking the upper part to focus control
				return true;
			}
			float value = event.x - rect.x;
			value *= float(this.maximum - this.minimum) / rect.w;
			return this.onUpdate(this, this.minimum + value);
		}
		return View.onMouseEvent(this, rect, event);
	}

	void onMeasure(Slider this, Rect rect&, const Style style&) {
		char value[64] = {0};
		if (this.divisor != 0) {
			value.append(0, this.valueF64(), style.fmtFlt);
		} else {
			value.append(0, this.value, style.fmtInt);
		}
		style.measure(&rect, this.text, length(value));
		View.onMeasure(this, &rect, style);
	}

	void onDrawSlider(Slider this, Image offs, Rect rect, const Style style&) {
		if (this.maximum == this.minimum) {
			return;
		}
		const int y0 = rect.y + rect.h - 1;
		const int y1 = y0 - style.padding / 4;
		const int x0 = rect.x;
		const int x1 = rect.x + rect.w;
		const int x = x0 + (this.value - this.minimum) * rect.w / (this.maximum - this.minimum);
		offs.fillRect(x, y0, x1, y1, style.focusedColor);
		offs.fillRect(x0, y0, x, y1, style.valueColor);
	}

	void onDraw(Slider this, Image offs, Rect rect, const Style style&) {
		char value[64] = {0};
		if (this.divisor != 0) {
			value.append(0, this.valueF64(), style.fmtFlt);
		} else {
			value.append(0, this.value, style.fmtInt);
		}
		if (this.onDrawSlider != null) {
			this.onDrawSlider(this, offs, rect, style);
		}
		View.onDraw(this, offs, rect, style, this.text, value);
	}
}

struct Widget: Layout {
	int visibleGroup = -1;
	bool measure = true;
	bool hidden = false;
	Style style;
	Image offs;
	Rect rect;

	// Mouse state
	int clicks;
	int64 time;
	int64 timeout;
	int32 button;
	int32 mouseX;
	int32 mouseY;

	// Keyboard state
	bool isShift = false;
	bool isCtrl = false;
	bool isAlt = false;
	int key = 0;
	int repeat = 0;

	static bool focusNext(View view, bool prev) {
		if (view == null) {
			// probably a separator
			return false;
		}
		if (!view.enabled) {
			// can not focus a disabled view
			return false;
		}
		if (!view.focusable) {
			// can not focus a non focusable view
			return false;
		}

		Layout this = view.as(Layout);
		if (this == null) {
			// view is not a layout, and is focusable
			return true;
		}

		int n = this.focusedIndex;
		if (prev) {
			if (n < 0) {
				n = this.itemCount - 1;
			}
			for (int i = n; i >= 0; i -= 1) {
				View view = this.items[i];
				Rect rect = this.rects[i];
				if (rect.w && rect.h && focusNext(view, prev)) {
					if (view.as(Layout) != null || i != this.focusedIndex) {
						this.focusedIndex = i;
						return true;
					}
				}
			}
		} else {
			if (n < 0) {
				n = 0;
			}
			for (int i = n; i < this.itemCount; i += 1) {
				View view = this.items[i];
				Rect rect = this.rects[i];
				if (rect.w && rect.h && focusNext(view, prev)) {
					if (view.as(Layout) != null || i > this.focusedIndex) {
						this.focusedIndex = i;
						return true;
					}
				}
			}
		}

		this.clearState(Style.focused);
		return false;
	}
	static View getFocused(const Layout this) {
		if (this.focusedIndex < 0) {
			return null;
		}
		View selected = this.items[this.focusedIndex];
		if (Layout layout = selected.as(Layout)) {
			View view = getFocused(layout);
			if (view != null) {
				return view;
			}
		}
		return selected;
	}
	static bool isFocused(const Layout this, View items...) {
		if (this.focusedIndex < 0) {
			// nothing is focused.
			return false;
		}

		View view = this.items[this.focusedIndex];
		for (int i = 0; i < items.length; i += 1) {
			if (items[i] == view) {
				return true;
			}
		}

		if (Layout layout = view.as(Layout)) {
			return isFocused(layout, ...items);
		}
		return false;
	}
	static bool isPressed(const Layout this, View items...) {
		if (this.pressedIndex < 0) {
			// nothing is pressed.
			return false;
		}

		View view = this.items[this.pressedIndex];
		for (int i = 0; i < items.length; i += 1) {
			if (items[i] == view) {
				return true;
			}
		}

		if (Layout layout = view.as(Layout)) {
			return isPressed(layout, ...items);
		}
		return false;
	}

	static bool setGroup(Widget this, int group, bool selectFirst) {
		if (this.visibleGroup != group) {
			this.visibleGroup = group;
			if (selectFirst) {
				// measure then focus first visible view
				this.onMeasure(this, &this.rect, this.style);
				this.clearState(Style.focused);
				this.focusNext(false);
			}
			this.measure = true;
			return true;
		}
		return false;
	}
	inline setGroup(Widget this, int group) = setGroup(this, group, false);

	void onMeasure(Widget this, Rect rect&, const Style style&) {
		static void hideViews(Widget widget, Layout layout) {
			for (int i = 0; i < layout.itemCount; i += 1) {
				View view = layout.items[i];
				if (Layout child = view.as(Layout)) {
					hideViews(widget, child);
				}
				if ((widget.visibleGroup & view.visible) == 0) {
					layout.rects[i].w = 0;
					layout.rects[i].h = 0;
				}
			}
		}
		Layout.onMeasure(this, &rect, style);
		hideViews(this, this);
	}

	void onLayout(Widget this, Rect rect&) {
		if (rect.x < 0) {
			rect.x = this.offs.width() - rect.w + rect.x;
		}
		if (rect.y < 0) {
			rect.y = this.offs.height() - rect.h + rect.y;
		}
		Layout.onLayout(this, &rect);
	}

	void onDraw(Widget this, Image offs, Rect rect, const Style style&) {
//		offs = offs.slice(rect); // todo: clip drawings
		// draw background
		if (style.backgroundColor != -1) {
//			offs.fill(style.backgroundColor); // todo: clip drawings
			int top = rect.y - style.border;
			int left = rect.x - style.border;
			int right = rect.x + rect.w + style.border;
			int bottom = rect.y + rect.h + style.border;
			offs.fillRect(left, top, right, bottom, style.backgroundColor);
		}
		Layout.onDraw(this, offs, rect, style);
//		offs.destroy(); // todo: clip drawings
	}

	// TODO: remove: this override allows to forward the drag event to the focused view
	bool onMouseEvent(Widget this, const Rect rect&, const MouseEvent event&) {
		if (Layout.onMouseEvent(this, rect, event)) {
			return true;
		}

		if (!(event.button && (event.dx || event.dy))) {
			// event is not a drag event
			return false;
		}

		if (this.pressedIndex != -1) {
			// there is a pressed view, and is different than the focused one
			return false;
		}

		View view = getFocused(this);
		if (view == null) {
			// there is no focused view
			return false;
		}
		// forward the event to the view
		return view.onMouseEvent(view, rect, event);
	}

	static bool onEvent(Widget this, int action, int button, int x, int y) {
		if (action == Gui.WINDOW_INIT) {
			this.onCreate(this);
			// measure then focus first visible view
			this.onMeasure(this, &this.rect, this.style);
			this.clearState(Style.focused);
			this.focusNext(false);
			return true;
		}
		if (action == Gui.WINDOW_CLOSE) {
			// TODO: implement reference counted gc
			this.onDestroy(this);
			this.destroy();
			return false;
		}
		if (action == Gui.WINDOW_DRAW) {
			if (this.hidden) {
				return;
			}
			Rect rect& = this.rect;
			if (this.measure) {
				rect.w = rect.h = 0;
				this.onMeasure(this, &rect, this.style);
				this.onLayout(this, &rect);
				this.measure = false;
			}
			this.onDraw(this, this.offs, rect, this.style);
			return true;
		}
		if (action == Gui.WINDOW_ENTER) {
			// do not process event
			return false;
		}
		if (action == Gui.WINDOW_LEAVE) {
			// do not process event
			return false;
		}

		if (action == Gui.FINGER_PRESS) {
			// finger press results also a mouse event
			// skip the finger event
			return false;
		}
		if (action == Gui.FINGER_MOTION) {
			// translate to mouse event
			action = Gui.MOUSE_MOTION;
		}
		if (action == Gui.FINGER_RELEASE) {
			// finger release results also a mouse event
			// skip the finger event
			return false;
		}

		if (action == Gui.MOUSE_PRESS) {
			if (this.button != button || this.mouseX != x || this.mouseY != y) {
				this.clicks = 0;
			}
			int64 now = System.millis();
			if ((now - this.time) / 1000. > .5) {
				this.clicks = 0;
			}
			this.time = now;
			if (button == 1) {
				this.timeout = now + 1000;
			} else {
				this.timeout = 0;
			}
			this.button = button;
			this.mouseX = x;
			this.mouseY = y;
			this.clicks += 1;

			if (button != 1) {
				return false;
			}

			MouseEvent event = {
				release: false;
				isShift: this.isShift;
				isCtrl: this.isCtrl;
				isAlt: this.isAlt;
				clicks: this.clicks;
				button: button;
				x: x;
				y: y;
			};
			return this.onMouseEvent(this, this.rect, event);
		}
		if (action == Gui.MOUSE_MOTION) {
			int oldX = this.mouseX;
			int oldY = this.mouseY;
			this.time = System.millis();
			this.timeout = 0;
			this.button = button;
			this.mouseX = x;
			this.mouseY = y;
			this.clicks = 0;

			if (button != 0 && button != 1) {
				return false;
			}
			MouseEvent event = {
				release: false;
				isShift: this.isShift;
				isCtrl: this.isCtrl;
				isAlt: this.isAlt;
				clicks: this.clicks;
				button: button;
				dx: x - oldX;
				dy: y - oldY;
				x: x;
				y: y;
			};
			return this.onMouseEvent(this, this.rect, event);
		}
		if (action == Gui.MOUSE_RELEASE) {
			int64 now = System.millis();
			if ((now - this.time) / 1000. > .5) {
				this.clicks = 0;
			}
			this.timeout = 0;
			if (button != 1) {
				return false;
			}

			MouseEvent event = {
				release: true;
				isShift: this.isShift;
				isCtrl: this.isCtrl;
				isAlt: this.isAlt;
				clicks: this.clicks;
				button: button;
				x: x;
				y: y;
			};
			return this.onMouseEvent(this, this.rect, event);
		}

		if (action == Gui.KEY_PRESS) {
			this.isShift = (y & Gui.KEY_MASK_SHIFT) != 0;
			this.isCtrl = (y & Gui.KEY_MASK_CTRL) != 0;
			this.isAlt = (y & Gui.KEY_MASK_ALT) != 0;
			if (this.key != button) {
				this.key = button;
				this.repeat = 0;
			} else {
				this.repeat += 1;
			}

			if (button == '\t' || button == '\r') {
				// Tab, Enter: select prev or next control.
				if (!this.focusNext(this.isShift)) {
					return this.focusNext(this.isShift);
				}
				return true;
			}
			if (button == '[' || button == ']') {
				// hide, show controls
				Rect r& = this.rect;
				if (button == '[' && r.x == r.y) {
					this.hidden = !this.hidden;
					return true;
				}
				if (button == ']' && r.x != r.y) {
					this.hidden = !this.hidden;
					return true;
				}

				if (this.rect.x == this.rect.y) {
					r.x = -r.y;
				} else {
					r.x = r.y;
				}
				this.hidden = false;
				this.measure = true;
				return true;
			}
			if (button == 8 && this.isShift) {
				return this.onReset(this);
			}

			KeyEvent event = {
				release: false;
				isShift: this.isShift;
				isCtrl: this.isCtrl;
				isAlt: this.isAlt;
				repeat: this.repeat;
				key: button;
			};
			return this.onKeyEvent(this, event);
		}
		if (action == Gui.KEY_RELEASE) {
			this.isShift = (y & Gui.KEY_MASK_SHIFT) != 0;
			this.isCtrl = (y & Gui.KEY_MASK_CTRL) != 0;
			this.isAlt = (y & Gui.KEY_MASK_ALT) != 0;
			this.key = 0;
			KeyEvent event = {
				release: true;
				isShift: this.isShift;
				isCtrl: this.isCtrl;
				isAlt: this.isAlt;
				repeat: 0;
				key: button;
			};
			return this.onKeyEvent(this, event);
		}

		if (action == Gui.EVENT_TIMEOUT) {
			if (this.timeout > 0) {
				// long press
				MouseEvent event = {
					release: true;
					isShift: this.isShift;
					isCtrl: this.isCtrl;
					isAlt: this.isAlt;
					clicks: -1;
					button: this.button;
					x: this.mouseX;
					y: this.mouseY;
				};
				return this.onMouseEvent(this, this.rect, event);
			}
		}

		debug("event not processed", action);
		return false;
	}

	static int draw(Widget this) {
		if (this.timeout > 0) {
			int64 timeout = this.timeout - System.millis();
			this.onEvent(Gui.WINDOW_DRAW, 0, 0, 0);
			return Math.max(int64(0), timeout);
		}
		this.onEvent(Gui.WINDOW_DRAW, 0, 0, 0);
		return 0;
	}
}

Widget Widget(Image offs, int pos, const Style style&, View items...) {
	Widget layout = {
		offs: offs;
		rect: {
			x: pos;
			y: pos;
			w: -1;
			h: -1;
		};
		style: style;
		orientation: -1;
		alignment: Style.fill;
		itemCount: items.length;
		items: pointer.alloc(null, items.length * sizeof(View));
		rects: pointer.alloc(null, items.length * sizeof(Rect));
	};
	for (int i = 0; i < items.length; i += 1) {
		layout.items[i] = items[i];
	}

	if (pos < 0) {
		layout.rect.y = -pos;
	}
	return layout;
}
Widget Widget(Image offs, int pos, View items...) {
	Style style = {
		align: Style.left;
		fmtInt: { padLen: 4 };
		fmtFlt: { padLen: 4, precision: 2 };
	};
	return Widget(offs, pos, style, ...items);
}

struct Histogram: View {
	const bool lines = false;
	// histogram data is in alpha channel, display it with color rgb
	bool color = false;
	argb data[256] = null;   // FIXME: this should be not possible

	static void update(Histogram this, Image image) {
		uint32 data[*] = pointer(this.data);
		image.calcRgbHist(null, &data);
		this.color = false;
	}

	bool onUpdate(Histogram this, int idx, int value) {
		this.data[idx] = argb(idx, value, value, value);
		return true;
	}

	bool onReset(Histogram this) {
		bool result = false;
		for (int i = 0; i < 256; i += 1) {
			result |= Histogram.onUpdate(this, i, i);
		}
		return result;
	}

	void onCreate(Histogram this) {
		Histogram.onReset(this);
	}

	bool onMouseEvent(Histogram this, const Rect rect&, const MouseEvent event&) {
		if (event.release || !event.button) {
			return View.onMouseEvent(this, rect, event);
		}

		int px = Math.max(0, rect.w - 256) / 2;
		int x0 = event.x - rect.x - px;
		int x1 = x0 - event.dx;
		int y0 = event.y - rect.y;
		int y1 = y0 - event.dy;

		if (x0 > x1) {
			int x = x0;
			x0 = x1;
			x1 = x;
			int y = y0;
			y0 = y1;
			y1 = y;
		}
		int y = y0 << 16;
		int dy = y1 - y0;
		if (x1 != x0) {
			dy = (dy << 16) / (x1 - x0);
		}
		if (x0 < 0) {
			y -= x0 * dy;
			x0 = 0;
		}
		if (x1 > 256) {
			x1 = 256;
		}
		int h = rect.h;
		for (int x = x0; x < x1; x += 1) {
			int val = 255 * (h - (y >> 16)) / h;
			this.onUpdate(this, x, val);
			y += dy;
		}
		return true;
	}

	void onMeasure(Histogram this, Rect rect&, const Style style&) {
		if (this.width < 0) {
			rect.w = 256;
		}
		if (this.height < 0) {
			rect.h = 128;
		}
		return View.onMeasure(this, &rect, style);
	}

	void onDraw(Histogram this, Image offs, const Rect rect, const Style style&) {
		enum {
			colB: 0x4371c6;
			colG: 0x72d874;
			colR: 0xfc6165;
			colL: 0xdadada;
		}
		int px = (rect.w - 256) / 2;
		if (this.lines) {
			int y = rect.y + rect.h - 1;
			int pb = bch(this.data[0]) * rect.h / 256;
			int pg = gch(this.data[0]) * rect.h / 256;
			int pr = rch(this.data[0]) * rect.h / 256;
			int pl = ach(this.data[0]) * rect.h / 256;

			for (int i = 1; i < this.data.length; i += 1) {
				int x = rect.x + i + px;
				argb data = this.data[i];
				int bv = bch(data) * rect.h / 256;
				int gv = gch(data) * rect.h / 256;
				int rv = rch(data) * rect.h / 256;
				int lv = ach(data) * rect.h / 256;

				if (this.color) {
					offs.drawLine(x - 1, y - pl, x, y - lv, uint32(data));
				} else {
					offs.drawLine(x - 1, y - pb, x, y - bv, colB);
					offs.drawLine(x - 1, y - pg, x, y - gv, colG);
					offs.drawLine(x - 1, y - pr, x, y - rv, colR);
					offs.drawLine(x - 1, y - pl, x, y - lv, colL);
				}

				pb = bv;
				pg = gv;
				pr = rv;
				pl = lv;
			}
		} else {
			for (int i = 0; i < this.data.length; i += 1) {
				int x = rect.x + i + px;
				argb data = this.data[i];
				if (this.color) {
					int y1 = rect.y + rect.h;
					int y0 = y1 - ach(data) * rect.h / 256;
					offs.fillRect(x, y0 - 1, x + 1, y1, uint32(data));
				} else {
					int bv = bch(data) * rect.h / 256;
					int gv = gch(data) * rect.h / 256;
					int rv = rch(data) * rect.h / 256;
					int lv = ach(data) * rect.h / 256;
					for (int y = 0; y < rect.h; y += 1) {
						uint32 b = colB * uint32(y < bv);
						uint32 g = colG * uint32(y < gv);
						uint32 r = colR * uint32(y < rv);
						uint32 l = colL * uint32(y < lv);
						offs.set(x, rect.h - y + rect.y - 1, r | g | b | l);
					}
				}
			}
		}
	}
}
