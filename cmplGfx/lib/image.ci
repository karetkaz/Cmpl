/// Create an image with the given with, height and depth
inline Image(int width, int height, int depth) = Image.create(width, height, depth);

/// Create an image by copying another one
Image Image(Image copy) {
	Image result = Image.create(copy.width(), copy.height(), copy.depth());
	result.copy(0, 0, copy, null);
	return result;
}

/// Create an image by copying another one using a custom width and height
Image Image(Image image, int width, int height, int align, float64 maxScale) {
	enum: int {
		center: 0;
		left: 1;
		right: 2;
		top: 4;
		bottom: 8;
		fitVertical: left + right;
		fitHorizontal: top + bottom;
		fill: 0x10;
		highRes: 0x20;
	}

	float64 scale = 1;
	Image result = Image(width, height, image.depth());

	int alignVertical = align & fitVertical;
	int alignHorizontal = align & fitHorizontal;

	bool fitWidth = alignVertical == fitVertical;
	bool fitHeight = alignHorizontal == fitHorizontal;

	if (fitWidth && fitHeight) {
		float64 widthScale = width / float64(image.width());
		float64 heightScale = height / float64(image.height());
		if (align & fill) {
			scale = Math.max(widthScale, heightScale);
		} else {
			scale = Math.min(widthScale, heightScale);
		}
	}
	else if (fitWidth) {
		scale = width / float64(image.width());
	}
	else if (fitHeight) {
		scale = height / float64(image.height());
	}
	if (scale > maxScale) {
		scale = maxScale;
	}

	// calculate translation
	float64 tx = 0;
	if (fitWidth || alignVertical == center) {
		tx = (width - image.width() * scale) / 2;
	}
	else if (alignVertical == right) {
		tx = (width - image.width() * scale);
	}

	float64 ty = 0;
	if (fitHeight || alignHorizontal == center) {
		ty = (height - image.height() * scale) / 2;
	}
	else if (alignHorizontal == bottom) {
		ty = (height - image.height() * scale);
	}

	mat4f mat = {
		x: {x: 1 / scale, y: 0, z: 0, w: -tx / scale};
		y: {x: 0, y: 1 / scale, z: 0, w: -ty / scale};
		z: {x: 0, y: 0, z: 1 / scale, w: 0};
		w: {x: 0, y: 0, z: 0, w: 1};
	};

	result.transform(null, image, null, align & highRes ? 1 : 0, mat.data);
	return result;
}

/// Create an image by opening an image file
Image Image(const char fileName[*], int depth) {
	if (fileName.endsWith(".bmp", ignCaseCmp)) {
		return Image.openBmp(fileName, depth);
	}
	return Image.openImg(fileName, depth);
}

/// Create an image by opening an image file
inline Image(const char fileName[*]) = Image(fileName, 32);

/// Create an image by opening an image file with custom width and height
Image Image(const char fileName[*], int width, int height, int depth, int align, float64 maxScale) {
	Image image = Image(fileName, depth);
	Image result = Image(image, width, height, align, maxScale);
	image.destroy();
	return result;
}

/// Create an image by opening an image file with custom width and height
inline Image(const char fileName[*], int width, int height, int depth, int align) = Image(fileName, width, height, depth, align, Math.inf);

/// Create an image by opening an image file with custom width and height
inline Image(const char fileName[*], int width, int height, int depth) = Image(fileName, width, height, depth, -1, Math.inf);

/// Set the the color of a pixel at the given position
inline set(Image image, int32 x, int32 y, argb color) = Image.set(image, x, y, uint32(color));

/// apply the 2d lookup table to the image
inline colorMap(Image image, const Rect roi&, const argb lut[256]) = Image.colorMap(image, roi, pointer(lut));

///@public
inline resize(Image image, const Rect rect&, const Image src, const Rect roi&, int interpolate) = Image.transform(image, rect, src, roi, interpolate, null);

///@public
inline slice(Image image, const Rect rect&) = Image.slice(Image(0, 0, image.depth()), image, rect);

///@public
bool clip(Image src, Rect roi&) {

	roi.w += roi.x;
	roi.h += roi.y;

	if (roi.x < 0) {
		roi.x = 0;
	}

	if (roi.y < 0) {
		roi.y = 0;
	}

	int width = src.width();
	if (roi.w > width) {
		roi.w = width;
	}

	int height = src.height();
	if (roi.h > height) {
		roi.h = height;
	}

	roi.w -= roi.x;
	roi.h -= roi.y;

	if (roi.w <= 0) {
		return false;
	}
	if (roi.h <= 0) {
		return false;
	}
	return true;
}

/// Fill the entire image with the given color
inline fill(Image image, uint32 color) = image.fillRect(0, 0, 65536, 65536, color);

/// Fill the surface by evaluating the given function for each pixel
void eval(Image image, const Rect roi&, float32 aspect, float32 time, vec4f eval(vec4f in)) {
	Rect rect = {
		x: roi != null ? roi.x : 0;
		y: roi != null ? roi.y : 0;
		w: roi != null ? roi.w : image.width();
		h: roi != null ? roi.h : image.height();
	};

	if (!image.clip(&rect)) {
		return;
	}

	float32 w = image.width();
	float32 h = image.height();
	if (aspect == 0) {
		aspect = w / h;
	}
	h *= aspect;
	vec4f in = {
		x: 0;
		y: 0;
		z: 0;
		w: time;
	};
	for (int y = rect.y; y < rect.y + rect.h; y += 1) {
		in.y = y / h;
		for (int x = rect.x; x < rect.x + rect.w; x += 1) {
			in.x = x / w;
			image.set(x, y, argb(eval(in)));
		}
	}
}

///@public
void tile(Image image, int x, int y, Image src, const Rect roi&) {
	Rect rect = {
		x: roi != null ? roi.x : 0;
		y: roi != null ? roi.y : 0;
		w: roi != null ? roi.w : src.width();
		h: roi != null ? roi.h : src.height();
	};

	if (!src.clip(&rect)) {
		return;
	}

	if (x < 0) {
		x = -(-x % rect.w);
	}
	if (y < 0) {
		y = -(-y % rect.h);
	}
	int width = image.width();
	int height = image.height();
	for (int j = y; j < height; j += rect.h) {
		for (int i = x; i < width; i += rect.w) {
			image.copy(i, j, src, rect);
		}
	}
}

/// Fill the given rectangle rounding the corners using the given radius
void fillRect(Image image, int x1, int y1, int x2, int y2, int rx, int ry, uint32 color) {
	if (x1 > x2) {
		int t = x1;
		x1 = x2;
		x2 = t;
	}
	if (y1 > y2) {
		int t = y1;
		y1 = y2;
		y2 = t;
	}

	inline clamp(int value, int min, int max) = value < min ? min : value > max ? max : value;
	rx = clamp(rx, 1, (x2 - x1) / 2);
	ry = clamp(ry, 1, (y2 - y1) / 2);

	int sx = rx * rx;
	int sy = ry * ry;

	int r = sx * ry / 2;
	int rdy = r * 2;
	int rdx = 0;

	int rx1 = x1 + rx;
	int rx2 = x2 - rx;

	for (;y1 < y2;) {
		image.fillRect(rx1, y1, rx2, y1, color);
		image.fillRect(rx1, y2, rx2, y2, color);
		if (r >= 0) {
			rx1 -= 1;
			rx2 += 1;
			r -= rdx += sy;
		}
		if (r < 0) {
			y1 += 1;
			y2 -= 1;
			r += rdy -= sx;
		}
	}
	image.fillRect(x1, y1, x2, y2, color);
}

/// Fill the given rectangle rounding the corners using the given radius
inline fillRect(Image image, int x1, int y1, int x2, int y2, int r, uint32 color) = fillRect(image, x1, y1, x2, y2, r, r, color);

/// Fill the given rectangle rounding the corners using the given radius
void fillRect(Image image, const Rect roi&, int rx, int ry, uint32 color) {
	if (roi.w <= 0 || roi.h <= 0) {
		// empty rectangle
		return;
	}
	image.fillRect(roi.x, roi.y, roi.x + roi.w - 1, roi.y + roi.h - 1, rx, ry, color);
}

/// Fill the given rectangle rounding the corners using the given radius
inline fillRect(Image image, const Rect roi&, int r, uint32 color) = fillRect(image, roi, r, r, color);

/// Fill the entire image with the given color
void drawRect(Image image, const Rect roi&, uint32 color) {
	if (roi.w <= 0 || roi.h <= 0) {
		// empty rectangle
		return;
	}
	image.drawRect(roi.x, roi.y, roi.x + roi.w - 1, roi.y + roi.h - 1, color);
}

/// Fill the entire image with the given color
void fillRect(Image image, const Rect roi&, uint32 color) {
	if (roi.w <= 0 || roi.h <= 0) {
		// empty rectangle
		return;
	}
	image.fillRect(roi.x, roi.y, roi.x + roi.w - 1, roi.y + roi.h - 1, color);
}

