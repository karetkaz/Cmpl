// Image geometry transformation demo.

gxSurf font = gxSurf.openFnt("asset/font/modern-1.fnt");
gxSurf in = gxSurf("asset/image/forest.png", 32);
gxSurf offs = gxSurf("asset/image/texture_nature_01.png", in.width(), in.height(), 32);

static if (typename(gxSurf.transform) == null) {
void transform(gxSurf surf, const gxRect rect&, gxSurf src, const gxRect roi&, int32 interpolate, float32 mat[16]) {
	assert(roi == null);
	assert(rect != null);

	gxRect dRec = {
		x: rect != null ? rect.x : 0;
		y: rect != null ? rect.y : 0;
		w: rect != null ? rect.w : surf.width();
		h: rect != null ? rect.h : surf.height();
	};

	gxRect sRec = {
		x: roi != null ? roi.x : 0;
		y: roi != null ? roi.y : 0;
		w: roi != null ? roi.w : src.width();
		h: roi != null ? roi.h : src.height();
	};

	float32 xx = mat != null ? mat[0] : float32(sRec.w) / dRec.w;
	float32 xy = mat != null ? mat[1] : 0;
	float32 xt = mat != null ? mat[3] : sRec.x;
	float32 yy = mat != null ? mat[5] : float32(sRec.h) / dRec.h;
	float32 yx = mat != null ? mat[4] : 0;
	float32 yt = mat != null ? mat[7] : sRec.y;

	if (!surf.clip(dRec)) {
		// nothing to set
		return;
	}

	if (!src.clip(sRec)) {
		// nothing to get
		return;
	}

	for (int y = 0; y < dRec.h; y += 1) {
		int dy = dRec.y + y;
		float32 sy = sRec.y + y;
		for (int x = 0; x < dRec.w; x += 1) {
			int dx = dRec.x + x;
			float32 sx = sRec.x + x;
			float32 tx = (xx * sx + xy * sy + xt) / src.width();
			float32 ty = (yx * sx + yy * sy + yt) / src.height();
			surf.set(dx, dy, argb(src.tex(tx, ty)));
		}
	}
}
}

/// compute the the transformation matrix to center/crop/fit an image to a new size.
mat4f fitMatrix(const gxSurf src, int newWidth, int newHeight, bool fitWidth, bool fitHeight, double maxScale, double maxAspect) {
	double scale = 1;
	double widthScale = newWidth / double(src.width());
	double heightScale = newHeight / double(src.height());

	if (fitWidth && fitHeight) {
		scale = Math.min(widthScale, heightScale);
	}
	else if (fitWidth) {
		scale = widthScale;
	}
	else if (fitHeight) {
		scale = heightScale;
	}
	if (scale > maxScale) {
		scale = maxScale;
	}

	if (maxAspect > 0) {
		if (maxAspect > 1) {
			maxAspect = 1 / maxAspect;
		}
		double ratio = src.width() / double(src.height());
		if (ratio > 1) {
			ratio = 1 / ratio;
		}
		// debug("new aspect", ratio);
		if (ratio > maxAspect) {
			scale *= double.sqrt(maxAspect / ratio);
		}
	}

	// calculate translation
	double dx = (newWidth - src.width() * scale) / 2;
	double dy = (newHeight - src.height() * scale) / 2;

	return {
		x: {x: 1 / scale, y: 0, z: 0, w: -dx / scale};
		y: {x: 0, y: 1 / scale, z: 0, w: -dy / scale};
		z: {x: 0, y: 0, z: 1 / scale, w: 0};
		w: {x: 0, y: 0, z: 0, w: 1};
	};
}

mat4f skewMatrix(float sx, float sy) {
	return {
		x: {x: 1, y: float.tan(sx), z: 0, w: 0};
		y: {x: float.tan(sy), y: 1, z: 0, w: 0};
		z: {x: 0, y: 0, z: 1, w: 0};
		w: {x: 0, y: 0, z: 0, w: 1};
	};
}

int onEvent(int action, int button, int x, int y) {
	inline "/lib/gfx/micro.ui.ci";

	static Checker quality = {
		text: "high quality";
		checked: true;
	};
	static Checker flipX = {
		text: "flip x";
	};
	static Checker flipY = {
		text: "flip y";
	};
	static Slider rotate = {
		text: "rotate";
		maximum: 360;
	};
	static Slider skewX = {
		text: "skew x";
		minimum: -45;
		maximum: +45;
	};
	static Slider skewY = {
		text: "skew y";
		minimum: -45;
		maximum: +45;
	};
	static Slider scaleX = {
		text: "scale x";
		value: 100;
		minimum: 10;
		maximum: 500;
		divisor: 100;
	};
	static Slider scaleY = {
		text: "scale y";
		value: 100;
		minimum: 10;
		maximum: 500;
		divisor: 100;
	};
	static Slider translateX = {
		text: "translate x";
		minimum: -offs.width();
		maximum: +offs.width();
	};
	static Slider translateY = {
		text: "translate y";
		minimum: -offs.height();
		maximum: +offs.height();
	};

	static Slider maxRatio = {
		text: "max ratio";
		value: in.width() * 100 / in.height();
		minimum: 100;
		maximum: 400;
		divisor: 100;
	};
	static Checker fitWidth = {
		text: "fit width";
	};
	static Checker fitHeight = {
		text: "fit height";
	};
	static Checker drawRect = {
		text: "draw rect";
		checked: true;
	};
	static Slider imgRatio = {
		text: "ratio";
		enabled: false;
		divisor: 100;
		value: in.width() * imgRatio.divisor / in.height();
	};

	static Layout ui = Layout(offs, font,
		quality,
		fitWidth,
		fitHeight,
		maxRatio,
		scaleX, scaleY,
		skewX, skewY,
		flipX, flipY,
		rotate,
		translateX, translateY,
		drawRect,
		imgRatio
	);

	bool redraw = uiEvent(ui, action, button, x, y);
	if (action == Gui.KEY_PRESS) {
		if (button == 27) {
			return -1;
		}
	}
	if (redraw) {
		gxRect to = {
			w: offs.width() - 100;
			h: offs.height() - 100;
			x: (offs.width() - to.w) / 2;
			y: (offs.height() - to.h) / 2;
		};
		vec4f center = vec4f(in.width() / 2, in.height() / 2, 0, 0);
		mat4f mat = translation(center, 1)
			.mul(scale(vec4f(scaleX.val(), scaleY.val(), 0, 1), 1f / 100))
			.mul(skewMatrix(Math.radians(skewX.val()), Math.radians(skewY.val())))
			.mul(rotation(vec4f(0, 1, 0, 0), Math.radians(int(flipX.checked) * 180)))
			.mul(rotation(vec4f(1, 0, 0, 0), Math.radians(int(flipY.checked) * 180)))
			.mul(rotation(vec4f(0, 0, -1, 0), Math.radians(rotate.val())))
			.mul(translation(vec4f(translateX.val(), translateY.val(), 0, 0), 1))
			.mul(translation(center, -1))
			.mul(fitMatrix(in, to.w, to.h, fitWidth.checked, fitHeight.checked, 1, maxRatio.val() / 100.));
		debug("transform", mat.m);
		offs.transform(to, in, null, quality.checked ? 1 : 0, mat.data);
		if (drawRect.checked) {
			// draw interior
			to.w -= 1;
			to.h -= 1;
			offs.drawRect(to, 0);
		}
		ui.draw(30, 30);
	}
	return 0;
}

offs.show(onEvent);
font.destroy();
offs.destroy();
in.destroy();
