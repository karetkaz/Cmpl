/* Manual color lookup editing
 *
 * use 'tab' / 'shift' + 'tab' to select next control
 * use 'enter' / 'shift' + 'enter' to select next control
 * use '+' / '-' to adjust value of selected control (use shift for more precision)
 * use 'backspace' / 'shift' + 'backspace' to reset selected / every control value
 * use 'space' to press a button, toggle switch or reset slider value
 * use '[' or ']' to show and hide the controls
 *
 * drag the mouse left or right in the window to adjust selected slider value
 * TODO: add and move curve control points
 */

Image back = Image("asset/image/forest.jpg", 32);
Image offs = Image(back);

bool onButtonClick(Button this);
bool onCurveUpdate(Histogram this, int idx, int value);

Histogram histogram = {
	enabled: false;
};
Histogram curves = {
	height: 256;
	lines: true;
	focusable: false;
	onUpdate: onCurveUpdate;
};

Button btnLuminosity = {
	onReset: onButtonClick;
	text: "luminosity";
};
Button btnRgb = {
	onReset: onButtonClick;
	text: "rgb";
};
Button btnRed = {
	onReset: onButtonClick;
	text: "red";
};
Button btnGreen = {
	onReset: onButtonClick;
	text: "green";
};
Button btnBlue = {
	onReset: onButtonClick;
	text: "blue";
};

Button btnDump = {
	onClick: onButtonClick;
	text: "dump";
	focusable: false;
};
Button btnReset = {
	onClick: onButtonClick;
	text: "reset";
	focusable: false;
};

DebugView debug = {
	focusable: false;
	measures: -1;
	draws: -1;
};

void onProcess(FxWidget widget) {
	debug.startTimer();

	offs.copy(0, 0, back, null);
	if (widget.showOriginal) {
		histogram.update(offs);
		return;
	}

	offs.colorMap(null, curves.data);
	histogram.update(offs);
}
bool onCurveUpdate(Histogram this, int idx, int value) {
	Widget ui = this.getWidget();
	argb data& = this.data[idx];
	if (btnLuminosity.isFocused()) {
		data.a = sat_s8(value);
		return true;
	}
	if (btnRed.isFocused()) {
		data.r = sat_s8(value);
		return true;
	}
	if (btnGreen.isFocused()) {
		data.g = sat_s8(value);
		return true;
	}
	if (btnBlue.isFocused()) {
		data.b = sat_s8(value);
		return true;
	}
	if (btnRgb.isFocused()) {
		data.r = data.g = data.b = sat_s8(value);
		return true;
	}
	return false;
}
bool onButtonClick(Button this) {
	if (this == btnDump) {
		static if (typename(File) != null) {
			const FormatFlags fmt = {
				padChr: '0';
				width: 8;
			};
			char str[32] = {0};
			File.out.write("\nint32_t lookup[256] = {\n");
			for (int i = 0; i < curves.data.length; i += 1) {
				uint32 col = curves.data[i].v;
				if (i > 0) {
					File.out.write(",\n");
				}
				int len = str.append(str.append(0, "\t0x"), 16, col, fmt);
				File.write(File.out, str[... len]);
			}
			File.out.write("\n};\n");
		}
		return true;
	}
	for (int i = 0; i < 256; i += 1) {
		argb data& = curves.data[i];
		if (this == btnLuminosity) {
			data.a = i;
		}
		else if (this == btnRgb) {
			data.r = i;
			data.g = i;
			data.b = i;
		}
		else if (this == btnRed) {
			data.r = i;
		}
		else if (this == btnGreen) {
			data.g = i;
		}
		else if (this == btnBlue) {
			data.b = i;
		}
		else {
			curves.data[i] = argb(i, i, i, i);
		}
	}
	return true;
}

Window.show(offs, FxWidget(onProcess,
	histogram, curves,

	null,
	btnLuminosity, btnRgb, btnRed, btnGreen, btnBlue,
	btnDump, btnReset,

	null, debug
));
offs.destroy();
back.destroy();
