/* Blur, sharpen and blend images with alpha mask
 *
 * use 'tab' / 'shift' + 'tab' to select next control
 * use 'enter' / 'shift' + 'enter' to select next control
 * use '+' / '-' to adjust value of selected control (use shift for more precision)
 * use 'backspace' / 'shift' + 'backspace' to reset selected / every control value
 * use 'space' to press a button, toggle switch or reset slider value
 * use '[' or ']' to show and hide the controls
 *
 * drag the mouse up or down in the window to adjust control value
 */

Image font = Image.openFnt("asset/font/modern-1.fnt");
Image surf = Image("asset/image/forest.png", 32);
Image offs = Image(surf);
Image blur = Image(surf);

static if (typename(Image.blur) == null) {
inline "/lib/gfx/image/blur.ci";
}

static if (typename(Image.blend) == null) {
inline "/lib/gfx/image/blend.ci";
}

//inline blur(Image surf, int radius) = surf.blur(radius, 0.3 * (radius - 1) + 0.8);
inline blur(Image surf, float64 sigma) = surf.blur(Math.max(1., (sigma - 0.8) / 0.3 + 1), sigma);

int onEvent(int action, int button, int x, int y);
offs.show(onEvent);

blur.destroy();
offs.destroy();
surf.destroy();
font.destroy();

int onEvent(int action, int button, int x, int y) {
	inline "/lib/gfx/micro.ui.ci";

	static bool onMouseEvent(Button view, const MouseEvent event&);
	static bool onResetClick(Button view);
	static Histogram histogram = {
		image: offs;
		enabled: false;
	};
	static Switch autoUpdate = {
		text: "preview";
		checked: true;
		selectable: false;
	};

	static Slider sldBlur = {
		text: "Blur";
		value: 2000;
		minimum: 30;
		maximum: 2000;
		divisor: 100;
	};
	static Slider sldSharpness = {
		text: "Sharp";
		value: 512;
		minimum: 0;
		maximum: 512;
		divisor: 512;
	};

	static Button btnLinear = {
		text: "Linear";
		onMouseEvent: onMouseEvent;
	};
	static Button btnReflect = {
		text: "Reflect";
		onMouseEvent: onMouseEvent;
	};
	static Button btnRadial = {
		text: "Radial";
		onMouseEvent: onMouseEvent;
	};
	static Button btnSquare = {
		text: "Square";
		onMouseEvent: onMouseEvent;
	};

	static Slider sldHardness = {
		text: "Hardness";
		minimum: -255;
		maximum: 255;
		group: 1;
		selectable: false;
	};
	static Slider sldAlpha = {
		text: "Alpha";
		minimum: -255;
		maximum: 255;
		value: 255;
		group: 1;
		selectable: false;
	};
	static Switch chkMoveScale = {
		text: "Drag mode";
		off: "move";
		on: "scale";
		group: 1;
		selectable: false;
	};
	static Switch chkRect = {
		text: "Show rect";
		group: 1;
		selectable: false;
	};
	static Switch chkMask = {
		text: "Show mask";
		group: 1;
		selectable: false;
	};
	static Button btnReset = {
		text: "Reset rect";
		group: 1;
		selectable: false;
		onClick: onResetClick;
	};

	static Slider time = {
		text: "time";
		enabled: false;
	};
	static Button separator = {
		text: null;
		height: 3;
		enabled: false;
	};
	static Button separator1 = {
		text: null;
		height: 3;
		group: 1;
		enabled: false;
	};

	static Layout ui = Layout(offs, -30, 30, font,
		histogram, separator,
		sldBlur, sldSharpness,
		btnLinear, btnReflect, btnRadial, btnSquare,
		separator1, sldHardness, sldAlpha, chkMoveScale, chkMask, chkRect, btnReset,
		separator, autoUpdate, time);

	static int oldRadius = 0;
	static Rect rect = {
		x: 0;
		y: 0;
		w: offs.width();
		h: offs.height();
	};
	static bool onMouseEvent(Button view, const MouseEvent event&) {
		if (ui.activeIndex != -1) {
			// clicked on a ui element
			return false;
		}
		if (event.release) {
			// mouse button released
			return false;
		}
		if (event.isAlt) {
			rect.w -= event.dx;
			rect.h -= event.dy;
			return true;
		}
		if (chkMoveScale.checked == event.isShift) {
			rect.x -= event.dx;
			rect.y -= event.dy;
			return true;
		}
		rect.x += event.dx;
		rect.y += event.dy;
		rect.w -= 2 * event.dx;
		rect.h -= 2 * event.dy;
		return true;
	}
	static bool onResetClick(Button view) {
		rect.x = 0;
		rect.y = 0;
		rect.w = offs.width();
		rect.h = offs.height();
		return true;
	}

	if (action == Gui.KEY_PRESS) {
		if (button == 27) {
			return -1;
		}
	}
	if (ui.uiEvent(action, button, x, y)) {
		// clamp input values
		int amountValue = -sldSharpness.val();
		int radiusValue = sldBlur.val();

		uint64 start = System.millis();
		if (autoUpdate.checked) {
			int hardness = sldHardness.val();
			int alpha = sldAlpha.val();
			uint32 colors[512];

			if (oldRadius != radiusValue) {
				oldRadius = radiusValue;
				blur.copy(0, 0, surf, null);
				blur.blur(sldBlur.valFlt());
			}
			if (ui.isSelected(sldBlur)) {
				offs.copy(0, 0, blur, null);
			}
			else if (ui.isSelected(sldSharpness)) {
				offs.copy(0, 0, surf, null);
				offs.blend(0, 0, blur, null, amountValue, false, null);
			}
			else if (ui.isSelected(btnReflect)) {
				if (chkMask.checked) {
					offs.fill(0xff0000);
				} else {
					offs.copy(0, 0, blur, null);
				}
				alphaLut(colors, hardness, alpha, true);
				offs.gradient(&rect, gradient.MaskLinear, false, ...colors);
				offs.blend(0, 0, surf, null, 256, true, null);
			}
			else if (ui.isSelected(btnLinear)) {
				if (chkMask.checked) {
					offs.fill(0xff0000);
				} else {
					offs.copy(0, 0, blur, null);
				}
				alphaLut(colors, hardness, alpha, false);
				offs.gradient(&rect, gradient.MaskLinear, false, ...colors);
				offs.blend(0, 0, surf, null, 256, true, null);
			}
			else if (ui.isSelected(btnRadial)) {
				if (chkMask.checked) {
					offs.fill(0xff0000);
				} else {
					offs.copy(0, 0, blur, null);
				}
				alphaLut(colors, hardness, alpha, false);
				offs.gradient(&rect, gradient.MaskRadial, false, ...colors);
				offs.blend(0, 0, surf, null, 256, true, null);
			}
			else if (ui.isSelected(btnSquare)) {
				if (chkMask.checked) {
					offs.fill(0xff0000);
				} else {
					offs.copy(0, 0, blur, null);
				}
				alphaLut(colors, hardness, alpha, false);
				offs.gradient(&rect, gradient.MaskSquare, false, ...colors);
				offs.blend(0, 0, surf, null, 256, true, null);
			}
		}

		offs.calcHist(null, 0x00ffffff, &histogram.data);
		time.value = System.millis() - start;

		if (chkRect.checked) {
			offs.drawRect(rect, 0xff00ff);
		}

		if (ui.isSelected(btnReflect, btnLinear, btnRadial, btnSquare)) {
			ui.setGroup(1);
		} else {
			ui.setGroup(2);
		}
		ui.draw();
	}
	return 0;
}
