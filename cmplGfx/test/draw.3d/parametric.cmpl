// generate mesh by evaluating the given parametric function
Image texture = Image("../asset/image/texture.png", 32);
Mesh model = Mesh.create(0);

void eval(Mesh mesh, int sGrid, int tGrid, float64 epsilon, vec4f pos(float64 s, float64 t)) {
	bool sClosed = false;
	bool tClosed = false;

	if (sGrid < 0) {
		sGrid = -sGrid;
		sClosed = true;
	}
	if (tGrid < 0) {
		tGrid = -tGrid;
		tClosed = true;
	}

	float64 sDivision = sGrid - int(!sClosed);
	float64 tDivision = tGrid - int(!tClosed);

	mesh.recycle(sGrid * tGrid);
	for (int i = 0; i < tGrid; i += 1) {
		float64 t = i / tDivision;
		for (int j = 0; j < sGrid; j += 1) {
			float64 s = j / sDivision;

			vec4f pos = pos(s, t);
			vec4f posDs = pos(s + epsilon, t);
			vec4f posDt = pos(s, t + epsilon);

			posDs.x = (pos.x - posDs.x) / epsilon;
			posDs.y = (pos.y - posDs.y) / epsilon;
			posDs.z = (pos.z - posDs.z) / epsilon;
			posDt.x = (pos.x - posDt.x) / epsilon;
			posDt.y = (pos.y - posDt.y) / epsilon;
			posDt.z = (pos.z - posDt.z) / epsilon;
			vec4f nrm = normalize(cross(posDs, posDt));

			int idx = mesh.addVertex(pos.x, pos.y, pos.z);
			mesh.setNormal(idx, nrm.x, nrm.y, nrm.z);
			mesh.setTexture(idx, s, t);
		}
	}

	for (int j = 0; j < tGrid - 1; j += 1) {
		int l1 = j * sGrid;
		int l2 = l1 + sGrid;
		for (int i = 0; i < sGrid - 1; i += 1) {
			int v1 = l1 + i;		// v1 -- v2
			int v2 = v1 + 1;		//  | \  |
			int v4 = l2 + i;		//  |  \ |
			int v3 = v4 + 1;		// v4 -- v3
			mesh.addFace(v1, v2, v3, v4);
		}
	}
	if (tClosed) {
		int l2 = 0;
		int l1 = sGrid * (tGrid - 1);
		for (int i = 0; i < sGrid - 1; i += 1) {
			int v1 = l1 + i;		// v1 -- v2
			int v2 = v1 + 1;		//  | \  |
			int v4 = l2 + i;		//  |  \ |
			int v3 = v4 + 1;		// v4 -- v3
			mesh.addFace(v1, v2, v3, v4);
		}
	}
	if (sClosed) {
		int i = sGrid - 1;
		for (int j = 0; j < tGrid - 1; j += 1) {
			int l1 = j * sGrid;
			int l2 = l1 + sGrid;
			int v1 = l1 + i;		// v1 -- v2
			int v2 = l1 + 0;		//  | \  |
			int v4 = l2 + i;		//  |  \ |
			int v3 = l2 + 0;		// v4 -- v3
			mesh.addFace(v1, v2, v3, v4);
		}
	}
}

vec4f ellipse(float64 s, float64 t) {
	enum: float64 {
		X: 1;
		Y: 1;
		Z: 1;
		smin: 0.;
		smax: 2 * Float64.pi;
		tmin: -Float64.pi / 2;
		tmax: +Float64.pi / 2;
	}

	s = Float64.mix(smin, smax, s);
	t = Float64.mix(tmin, tmax, t);

	return {
		x: X * Float64.cos(t) * Float64.cos(s);
		y: Y * Float64.cos(t) * Float64.sin(s);
		z: Z * Float64.sin(t);
	};
}
vec4f cylinder(float64 s, float64 t) {
	enum: float64 {
		sMin: 0;
		sMax: 1;
		tMin: 0;
		tMax: 2 * Float64.pi;
	}

	s = Float64.mix(sMin, sMax, s);
	t = Float64.mix(tMin, tMax, t);

	return {
		x: Float64.cos(t);
		y: Float64.sin(t);
		z: -s;
	};
}
vec4f peak(float64 s, float64 t) {
	enum: float64 {
		h: 10.;
		H: 20.;
		smin: -H;
		smax: +H;
		tmin: -H;
		tmax: +H;
	}
	s = Float64.mix(smin, smax, s);
	t = Float64.mix(tmin, tmax, t);
	float64 sst = Float64.sqrt(s*s + t*t);
	return {
		x: s / H;
		y: t / H;
		z: h / H * Float64.sin(sst) / sst;
	};
}
vec4f shell(float64 s, float64 t) {
	enum: float64 {
		N: 5;
		H: 3;
		tMin: -Float64.pi;
		tMax: +Float64.pi;
		sMin: 0.;
		sMax: 2. * Float64.pi;
	}

	s = Float64.mix(sMin, sMax, s);
	t = Float64.mix(tMin, tMax, t);

	float64 w = s / (2 * Float64.pi);
	return {
		x: w * Float64.cos(N * s) * (1 + Float64.cos(t));
		y: w * Float64.sin(N * s) * (1 + Float64.cos(t));
		z: w * Float64.sin(t) + H * (w * w);
	};
}


model.eval(128, 128, 1e-15, peak);
debug("model", model);

// material Dash
model.texture(texture);
model.ambient(0.588235, 0.588235, 0.588235);
model.diffuse(0.588235, 0.588235, 0.588235);
model.specular(0.898039, 0.898039, 0.898039);
model.shine(64);

vec4f center = { x: 0, y: 0, z: 0, w: 1 };
vec4f resize = { x: 1, y: 1, z: 1, w: 1 };
model.normalize(0, &center.data, &resize.data);

Window.show(1440, 900, 15, model);
texture.destroy();
model.destroy();
