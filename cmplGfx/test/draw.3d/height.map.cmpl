// convert height map image to mesh and draw it
Image heightMap = Image("../asset/image/heightmap.png", 32);
Image texture = Image("../asset/image/heightmap.png", 32);
Mesh model = Mesh.create(0);

void eval(Mesh mesh, Image depthMap) {
	int width = depthMap.width();
	int height = depthMap.height();

	mesh.recycle(width * height);
	for (int y = 0; y < height; y += 1) {
		float64 t = y / float64(height);
		for (int x = 0; x < width; x += 1) {
			float64 s = x / float64(width);

			float64 z = lum(depthMap.tex(s, t));
			int idx = mesh.addVertex(1 - s, 1 - t, z);
			mesh.setNormal(idx, 0, 0, 1);
			mesh.setTexture(idx, s, t);
		}
	}
	for (int j = 0; j < height - 1; j += 1) {
		int l1 = j * width;
		int l2 = l1 + width;
		for (int i = 0; i < width - 1; i += 1) {
			int v1 = l1 + i;		// v1 -- v2
			int v2 = v1 + 1;		//  | \  |
			int v4 = l2 + i;		//  |  \ |
			int v3 = v4 + 1;		// v4 -- v3
			mesh.addFace(v1, v2, v3, v4);
		}
	}
}

model.eval(heightMap);
debug("model", model);

// material Dash
model.texture(texture);
model.ambient(0.588235, 0.588235, 0.588235);
model.diffuse(0.588235, 0.588235, 0.588235);
model.specular(0.898039, 0.898039, 0.898039);
model.shine(64);

vec4f center = { x: 0, y: 0, z: 0, w: 1 };
vec4f resize = { x: 1, y: 1, z: .3, w: 1 };
model.normalize(0, &center.data, &resize.data);

Window.show(1440, 900, 15, model);
texture.destroy();
model.destroy();
