gxSurf font = gxSurf.openFnt("asset/font/modern-1.fnt");
gxSurf offs = gxSurf("asset/image/forest.png", 32);

gxSurf in = gxSurf(offs, 0);

/// compute the the transformation matrix to center/crop/fit an image to a new size.
mat4f fitMatrix(const gxSurf src, int newWidth, int newHeight, bool fitWidth, bool fitHeight, double maxScale, double maxAspect) {
	double scale = 1;
	double widthScale = newWidth / double(src.width());
	double heightScale = newHeight / double(src.height());

	if (fitWidth && fitHeight) {
		scale = Math.min(widthScale, heightScale);
	}
	else if (fitWidth) {
		scale = widthScale;
	}
	else if (fitHeight) {
		scale = heightScale;
	}
	if (scale > maxScale) {
		scale = maxScale;
	}

	if (maxAspect > 0) {
		if (maxAspect < 1) {
			maxAspect = 1 / maxAspect;
		}
		double newAspect = src.width() / double(src.height());
		if (newAspect < 1) {
			newAspect = 1 / newAspect;
		}
		// debug("new aspect", newAspect);
		if (newAspect < maxAspect) {
			scale *= newAspect / maxAspect;
		}
	}

	// calculate translation
	double dx = (newWidth - src.width() * scale) / 2;
	double dy = (newHeight - src.height() * scale) / 2;

	return {
		x: {x: 1 / scale, y: 0, z: 0, w: -dx / scale};
		y: {x: 0, y: 1 / scale, z: 0, w: -dy / scale};
		z: {x: 0, y: 0, z: 1 / scale, w: 0};
		w: {x: 0, y: 0, z: 0, w: 1};
	};
}

int onEvent(int action, int button, int x, int y) {
	inline "lib/micro.ui.ci";

	static Checker fitWidth = {
		text: "fit width";
		checked: true;
	};
	static Checker fitHeight = {
		text: "fit height";
		checked: true;
	};
	static Slider maxAspect = {
		text: "max aspect";
		value: 133;
		minValue: 100;
		maxValue: 400;
	};
	static Slider maxScale = {
		text: "max scale";
		value: 100;
		minValue: 100;
		maxValue: 500;
	};
	static Slider imgAspect = {
		text: "aspect";
		enabled: false;
		value: in.width() * 100 / in.height();
	};
	static Checker quality = {
		text: "high quality";
		checked: true;
	};
	static Slider rotate = {
		text: "rotate";
		minValue: 0;
		maxValue: 360;
		value: 0;
	};

	static Layout ui = Layout(offs, font, fitWidth, fitHeight, quality, rotate, maxScale, maxAspect, imgAspect);
	bool redraw = action == Gui.WINDOW_INIT;

	if (uiEvent(ui, action, button, x, y)) {
		redraw = true;
	}
	else if (action == Gui.KEY_PRESS) {
		if (button == 27) {
			return -1;
		}
	}

	if (redraw) {
		gxRect to = {
			w: 400;
			h: 400;
			x: (offs.width() - to.w) / 2;
			y: (offs.height() - to.h) / 2;
		};
		mat4f rot = rotation(vec4f(in.width() / 2, in.height() / 2, 1, 0), vec4f(0, 0, 1, 0), Math.radians(rotate.val()));
		mat4f fit = fitMatrix(in, to.w, to.h, fitWidth.checked, fitHeight.checked, maxScale.val() / 100., maxAspect.val() / 100.);
		mat4f mat = rot.mul(fit);
		//debug("transform", mat.m);
		offs.transform(to, in, null, quality.checked ? 1 : 0, mat.data);
		offs.drawRect(to, 0);
		ui.draw(30, 30);
	}

	return 0;
}

offs.show(onEvent);
font.destroy();
offs.destroy();
in.destroy();
