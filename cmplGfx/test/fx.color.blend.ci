/* Blend two images using a function and transparency
 *
 * use 'tab' / 'shift' + 'tab' to select next control
 * use 'enter' / 'shift' + 'enter' to select next control
 * use '+' / '-' to adjust value of selected control (use shift for more precision)
 * use 'backspace' / 'shift' + 'backspace' to reset selected / every control value
 * use 'space' to press a button, toggle switch or reset slider value
 * use '[' or ']' to show and hide the controls
 *
 * drag the mouse left or right in the window to adjust selected slider value
 */

Image base = Image("asset/image/forest.jpg", 32);
Image image = Image("asset/image/texture_nature_01.png", 32);
Image offs = Image(base);
Image blend = Image(image, offs.width(), offs.height(), -1, 1.);
int onEvent(int action, int button, int x, int y);
offs.show(onEvent);
offs.destroy();
base.destroy();
blend.destroy();
image.destroy();

inline "/cmplGfx/lib/micro.ui.ci";
struct BlendButton: Button {
	inline linear_dodge(float32 base, float32 blend) = Math.min(base + blend, 1f);
	inline linear_burn(float32 base, float32 blend) = Math.max(base + blend - 1, 0f);
	inline color_dodge(float32 base, float32 blend) = (blend == 1 ? blend : Math.min(base / (1 - blend), 1f));
	inline color_burn(float32 base, float32 blend) = (blend == 0 ? blend : Math.max((1 - ((1 - base) / blend)), 0f));

	vec4f blend(vec4f base, vec4f blend);
}

Switch chkReverse = {
	text: "reverse";
	focusable: false;
	width: 192;
};
Slider btnAlpha = {
	text: "alpha";
	focusable: false;
	value: 192;
	maximum: 256;
	divisor: 256;
};

BlendButton btnNormal = {
	static vec4f normal(vec4f base, vec4f blend) {
		return base;
	}
	text: "normal";
	blend: null;
};
BlendButton btnAddition = {
	static vec4f addition(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base + blend;
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "addition";
	blend: addition;
};
BlendButton btnSubtract = {
	static vec4f subtract(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base - blend;
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "subtract";
	blend: subtract;
};
BlendButton btnMultiply = {
	static vec4f multiply(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base * blend;
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "multiply";
	blend: multiply;
};
BlendButton btnDivide = {
	static vec4f divide(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base / blend;
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "divide";
	blend: divide;
};
BlendButton btnDifference = {
	static vec4f difference(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base > blend ? base - blend : blend - base;
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "difference";
	blend: difference;
};
BlendButton btnExclusion = {
	static vec4f exclusion(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = .5f - 2 * (base - .5f) * (blend - .5f);
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "exclusion";
	blend: exclusion;
};

BlendButton btnLighten = {
	static vec4f lighten(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base > blend ? base : blend;
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "lighten";
	blend: lighten;
};
BlendButton btnDarken = {
	static vec4f darken(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base < blend ? base : blend;
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "darken";
	blend: darken;
};
BlendButton btnLinearDodge = {
	static vec4f linearDodge(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = BlendButton.linear_dodge(base, blend);
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "linearDodge";
	blend: linearDodge;
};
BlendButton btnLinearBurn = {
	static vec4f linearBurn(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = BlendButton.linear_burn(base, blend);
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "linearBurn";
	blend: linearBurn;
};
BlendButton btnColorDodge = {
	static vec4f colorDodge(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = BlendButton.color_dodge(base, blend);
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "colorDodge";
	blend: colorDodge;
};
BlendButton btnColorBurn = {
	static vec4f colorBurn(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = BlendButton.color_burn(base, blend);
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "colorBurn";
	blend: colorBurn;
};

BlendButton btnScreen = {
	static vec4f screen(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = 1 - (1 - base) * (1 - blend);
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "screen";
	blend: screen;
};
BlendButton btnOverlay = {
	static vec4f overlay(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = base < .5f
			? (2 * base * blend)
			: (1 - 2 * (1 - base) * (1 - blend));
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "overlay";
	blend: overlay;
};
BlendButton btnSoftLight = {
	static vec4f softLight(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = blend < .5f
			? (2 * base * blend + base * base * (1 - 2 * blend))
			: (float32.sqrt(base) * (2 * blend - 1) + 2 * base * (1 - blend));
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "softLight";
	blend: softLight;
};
BlendButton btnHardLight = {
	static vec4f hardLight(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = blend <= .5f
			? (base * (2 * blend))
			: (1 - (1 - base) * (1 - 2 * (blend - .5f)));
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "hardLight";
	blend: hardLight;
};
BlendButton btnLinearLight = {
	static vec4f linearLight(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = blend < .5f
			? BlendButton.linear_burn(base, (2 * blend))
			: BlendButton.linear_dodge(base, (2 * (blend - .5f)));
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "linearLight";
	blend: linearLight;
};
BlendButton btnVividLight = {
	static vec4f vividLight(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = blend < .5f
			? BlendButton.color_burn(base, (2 * blend))
			: BlendButton.color_dodge(base, (2 * (blend - .5f)));
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "vividLight";
	blend: vividLight;
};
BlendButton btnPinLight = {
	static vec4f pinLight(vec4f base, vec4f blend) {
		inline op(float32 base, float32 blend) = blend < .5f
			? Math.min(base, 2 * blend)
			: Math.max(base, 2 *(blend - .5f));
		return {
			x: op(base.x, blend.x);
			y: op(base.y, blend.y);
			z: op(base.z, blend.z);
			w: op(base.w, blend.w);
		};
	}
	text: "pinLight";
	blend: pinLight;
};

Slider time = {
	text: "time";
	enabled: false;
	onDrawSlider: null;
};
Widget ui = Widget(offs,
	chkReverse,
	btnAlpha,
	btnNormal,
	btnAddition,
	btnSubtract,
	btnMultiply,
	btnDivide,
	btnDifference,
	btnExclusion,
	btnLighten,
	btnDarken,
	btnLinearDodge,
	btnLinearBurn,
	btnColorDodge,
	btnColorBurn,
	btnScreen,
	btnOverlay,
	btnSoftLight,
	btnHardLight,
	btnLinearLight,
	btnVividLight,
	btnPinLight,
	time
);

int onEvent(int action, int button, int x, int y) {
	if (action == Gui.KEY_RELEASE && button == 27) {
		// exit when releasing escape key
		return -1;
	}
	if (!ui.onEvent(action, button, x, y)) {
		// event not processed, wait for the next one
		return 0;
	}

	if (button == 0 && (action == Gui.MOUSE_MOTION || action == Gui.FINGER_MOTION)) {
		return ui.draw();
	}

	if (ui.pressedIndex != -1) {
		// redraw only ui, in case there is an active control
		return ui.draw();
	}

	uint64 start = System.millis();
	bool reverse = chkReverse.selected != 0;
	offs.copy(0, 0, reverse ? base : blend, null);
	if (BlendButton selected = ui.getFocused().as(BlendButton)) {
		offs.blend(0, 0, reverse ? blend : base, null, btnAlpha.value, false, selected.blend);
	}
	time.value = System.millis() - start;
	return ui.draw();
}
