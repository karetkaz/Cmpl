// simple raytracer adapted from Pixel Bender: raytracer.pbk
struct Light {
	const vec4f position;
	const vec4f ambient;
	const vec4f diffuse;
	const vec4f specular;
	// by default use constant light attenuation
	const vec4f attenuation = vec4f(1, 0, 0, 0);
}

struct Thing: object {
	const vec4f color;      // r, g, b, TODO: alpha
	const vec4f ambient;
	const vec4f diffuse;
	const vec4f specular;
	const float shininess = 50;
	const float reflection = 0;

	float intersect(const Thing this&, const vec4f pos, const vec4f dir);

	vec4f normal(const Thing this&, const vec4f pos);

	vec4f texture(const Thing this&, vec4f pos) {
		return this.color;
	}
}

struct Plane: Thing {
	const vec4f n;
	const float d;

	float intersect(const Plane this&, const vec4f pos, const vec4f dir) {
		float den = dp3(this.n, dir);
		inline eps = 1e-14f;

		if (-den < eps) {
			// use Math.abs(den) value for both sides of the plane
			// no intersection
			return Math.inf;
		}

		float distance = -(dp3(this.n, pos) + this.d) / den;
		if (distance < eps) {
			return Math.inf;
		}
		return distance;
	}

	vec4f normal(const Plane this&, const vec4f pos) {
		return this.n;
	}

	static vec4f checkBoard(const Plane this&, vec4f p) {
		vec4f n = vec4f(this.n.y, this.n.z, -this.n.x);
		float v = dp3(p, cross(n, this.n));
		float u = dp3(p, n);
		if (u < 0) { u = 1 - u; }
		if (v < 0) { v = 1 - v; }

		if ((Math.floor(u) + Math.floor(v)) % 2 == 0) {
			return mul(this.color, .5f);
		}
		return this.color;
	}
}

struct Sphere: Thing {
	const vec4f position;
	const float radius;

	float intersect(const Sphere this&, const vec4f pos, const vec4f dir) {
		vec4f dst = sub(pos, this.position);
		float R = this.radius;
		float B = dp3(dst, dir);
		float C = dp3(dst, dst) - R * R;
		float D = (B * B) - C;

		if (D <= 0) {
			// no intersection
			return Math.inf;
		}
		// intersection!

		return -B - float32.sqrt(D);
	}

	vec4f normal(const Sphere this&, const vec4f pos) {
		return normalize(sub(pos, this.position));
	}

	static vec4f checkBoard(const Sphere this&, vec4f pos) {
		inline s = 3 * this.radius;
		vec4f n = normal(this, pos);
		float u = s * (1 + float.atan2(n.y, n.z) / Math.pi);
		float v = s * (1 - Math.acos(n.x) / Math.pi);
		if ((Math.floor(u) + Math.floor(v)) % 2 == 0) {
			return mul(this.color, .5f);
		}
		return this.color;
	}
}

struct SkyDome: Sphere {
	const Image image;

	float intersect(const SkyDome this&, const vec4f pos, const vec4f dir) {
		return Math.abs(Sphere.intersect(this, pos, dir));
	}

	vec4f texture(const SkyDome this&, vec4f pos) {
		vec4f n = normal(this, pos);
		float u = (1 + float.atan2(n.x, n.z) / Math.pi) / 2;
		float v = Math.acos(n.y) / Math.pi;
		return this.image.tex(u, v);
	}
}

Light lights[] = {
	Light {
		position: vec4f(0, 2, -4);
		ambient: vec4f(.1);
		diffuse: vec4f(1);
		specular: vec4f(1);
	};
};

Thing things[20] = {
	SkyDome { // fake the environment with a huge sphere
		image: Image("asset/image/garden.png");
		position: vec4f(0, 0, 0);
		radius: 100;
		color: vec4f(.6, .6, .6);
		ambient: vec4f(.8);
		diffuse: vec4f(0);
		specular: vec4f(0);
	};
	Plane { // the floor is a marble chess board
		n: normalize(vec4f(0, 1, 0));
		d: 3;
		color: vec4f(.6, .6, .6);
		texture: Plane.checkBoard;
		ambient: vec4f(.1);
		diffuse: vec4f(.8);
		specular: vec4f(.5);
		reflection: .5;
	};
	Sphere { // put a shiny sphere in the middle
		position: vec4f(0, 1, -10);
		radius: 2;
		color: vec4f(.8, .8, .8);
		ambient: vec4f(.05);
		diffuse: vec4f(.1);
		specular: vec4f(1);
		reflection: 1;
	};
};

// make a bunch of random spheres
for (int i = 3; i < things.length; i += 1) {
	float fi = i * 11;
	Sphere sphere = {
		position: {
			x: float.sin(fi / 5) * 6;
			y: float.sin(fi / 4.1f) * 2.5f;
			z: -18 - float.sin(fi / 3.1f + 1.2f) * 10;
		};
		radius: float.pow(float.sin(fi / 1.34 + 65.3) * .5 + .5, 3) + .2;
		color: {
			x: float.cos(fi / 2.1 + 1.3) * .5 + .5;
			y: float.cos(fi / 0.1 + 1.3) * .5 + .5;
			z: float.cos(fi / 5.1 + 6.3) * .5 + .5;
		};
		ambient: vec4f(.1);
		diffuse: vec4f(.7);
		specular: vec4f(1);
		reflection: float.pow(float.sin(fi / 2.1 + 1.243) * .5 + .5, 5);
	};
	things[i] = sphere;
}

/// returns the distance of the closest intersection
float intersect(const vec4f pos, const vec4f dir, int hit&) {
	float result = Math.inf;
	for (int i = 0; i < things.length; i += 1) {
		float distance = things[i].intersect(pos, dir);
		if (distance > 0 && distance < result) {
			result = distance;
			hit = i;
		}
	}
	return result;
}

/// returns how much light is reflected by the object
vec4f shade(const Thing thing&, vec4f pos, vec4f dir, vec4f rayDir) {
	inline mad(vec4f a, float b, vec4f c, vec4f d) = add(a, mul(b, mul(c, d)));

	vec4f result = thing.ambient;
	for (int i = 0; i < lights.length; i += 1) {
		const Light light& = lights[i];
		vec4f dst = sub(light.position, pos);
		vec4f l = normalize(dst);
		float d = length(dst);
		int hitIdx;

		// exclude light if the ray intersects an object closer than the light
		if (intersect(pos, l, &hitIdx) > d) {

			float attn = 1 / light.attenuation.eval(d);
			float diffuse = dp3(dir, l);

			if (diffuse > 0) {
				vec4f r = sub(l, mul(2 * diffuse, dir));
				float specular = dp3(r, rayDir);

				if (specular > 0) {
					specular = float.pow(specular, thing.shininess);
					result = result.mad(attn * specular, thing.specular, light.specular);
				}

				result = result.mad(attn * diffuse, thing.diffuse, light.diffuse);
			}

			result = result.mad(attn, thing.ambient, light.ambient);
		}
	}
	return result;
}

/// returns reflection vector
inline reflect(vec4f v, vec4f n) = sub(v, mul(2 * dp3(v, n), n));

// returns the color of the ray-traced scene
vec4f raytrace(vec4f xy) {
	inline MAX_RAY_SHOTS = 100;

	// calculate direction vector for this pixel
	vec4f rayDir = normalize(vec4f(2 * xy.x - 1, -2 * xy.y + 1, -2));
	vec4f rayPos = vec4f(0);
	int hitIdx;

	vec4f result = vec4f(0);
	vec4f colorScale = vec4f(1);

	for (int depth = 0; depth < MAX_RAY_SHOTS; depth += 1) {
		float distance = intersect(rayPos, rayDir, &hitIdx);
		if (distance >= Math.inf) {
			// no intersection
			return result;
		}

		Thing thing& = things[hitIdx];
		vec4f hitPos = add(rayPos, mul(rayDir, distance));
		vec4f hitDir = thing.normal(hitPos);

		// material and texture color
		vec4f color = thing.texture(hitPos);

		// blend light with material color
		vec4f light = thing.shade(hitPos, hitDir, rayDir);
		result = result.add(mul(colorScale, mul(color, light)));

		if (thing.reflection <= 0) {
			// no reflection, stop ray tracing
			return result;
		}

		// blend according to reflectivity
		colorScale = mul(colorScale, mul(thing.reflection, color));

		// update next ray position an direction
		rayDir = normalize(reflect(rayDir, hitDir));
		rayPos = hitPos;
	}
	return result;
}

Image image = Image(512, 512, 32);
int64 time = System.millis();
image.eval(null, 0, 0, raytrace);
time = System.millis() - time;
debug("raytrace millis", time);
image.show();
image.destroy();
