// Biorhythm calculator

Datetime birthdate = Datetime(2000, 2, 29);
Timestamp now = Timestamp.now();

int32 onEvent(int32 action, int32 button, int32 x, int32 y);
Image img = Image(1440, 500, 32);
Window.show(img, onEvent);

struct Biorhythm {
	int32 color;
	int32 period;
	int32 period2 = 0;
	char label[];

	static float64 value(Biorhythm this!, Timestamp birth, Timestamp date) {
		float64 d = date.diff(birth, Timestamp.precision);
		float64 t = convert(d, Timestamp.precision, Timeunit.Days);

		if (this.period2 != 0) {
			float64 y1 = Float64.sin(2 * Float64.pi * t / this.period);
			float64 y2 = Float64.sin(2 * Float64.pi * t / this.period2);
			return (y1 + y2) / 2;
		}

		return Float64.sin(2 * Float64.pi * t / this.period);
	}
}

Biorhythm biorhythms[] = {
	{color: uint32(argb(234, 50, 35)), period: 23, label: "Physical"};
	{color: uint32(argb(68, 144, 247)), period: 33, label: "Intellectual"};
	{color: uint32(argb(242,168, 59)), period: 38, label: "Intuitive"};
	{color: 0xfeab00, period: 23, period2: 28, label: "Passion"};  // physical + emotional
	{color: 0x6f6f6f, period: 23, period2: 33, label: "Mastery"};  // physical + intellectual
	{color: 0x9466db, period: 28, period2: 33, label: "Wisdom"};   // emotional + intellectual
	// extra biorhythms, uncomment to see them too
//	{color: 0xdadada, period: 43, label: "Aesthetic"};
//	{color: 0xdadada, period: 53, label: "Spiritual"};
//	{color: 0xdadada, period: 48, label: "Self Awareness"};
//	{color: 0xdadada, period: 23, period2: 38, label: "Perception"};  // Perception: physical, intuitive
//	{color: 0xdadada, period: 28, period2: 38, label: "Psychic"};     // Psychic: emotional, intuitive
//	{color: 0xdadada, period: 33, period2: 38, label: "Success"};     // Success: intellectual, intuitive
};

void drawBiorhythm(Image dst, Timestamp birth, Timestamp time, int32 days, int32 first) {
	static NumberFormat fmt! = {};
	Image font = Window.font;
	int32 lineHeight = font.height() + 3;
	inline padding = 15;

	int32 imgWidth = dst.width();
	int32 imgHeight = dst.height();
	int32 padding2 = imgWidth / days / 2;
	int32 textPosition = imgHeight - padding - lineHeight * (biorhythms.length + 1);

	char text[128] = {...};

	// clear screen
	dst.fill(0);

	// day grid lines
	dst.fillRect(first * imgWidth / days, 0, (first + 1) * imgWidth / days, imgHeight, 0x528252);
	for (int32 i = 1; i < days; i += 1) {
		int32 y = imgHeight;
		int32 x = imgWidth * i / days;
		dst.drawLine(x, 0, x, y, 0x282828);
	}

	// draw graphs & labels
	for (int i = 0; i < biorhythms.length; i += 1) {
		int32 ox = 0;
		int32 oy = 0;
		for (int32 j = 0; j < days; j += 1) {
			Timestamp t = time.add(j - first, Timeunit.Days);
			float64 b = biorhythms[i].value(birth, t);
			int32 y = (imgHeight - 2 * padding) * (1 - b) / 2 + padding;
			int32 x = imgWidth * j / days + padding2;
			if (j > 0) {
				drawLineIncl(dst, ox, oy, x, y, biorhythms[i].color);
			}
			ox = x;
			oy = y;
		}

		int64 value = 100 * biorhythms[i].value(birth, time);
		textPosition += lineHeight;
		int p = text.append(0, biorhythms[i].label);
		p = text.append(p, ": ");
		p = fmt.format(text, p, value);
		p = text.append(p, "%");
		dst.drawText(padding, textPosition, font, text, biorhythms[i].color);
	}

	// coordinate
	drawLine(dst, 0, imgHeight / 2, imgWidth, imgHeight / 2, 0x989898);
	drawLine(dst, imgWidth * first / days, 0, imgWidth * first / days, imgHeight, 0x989898);

	Datetime(time).format(text, 0, "Target date: %Y.%m.%d / %A");
	dst.drawText(padding, padding, font, text, 0x528252);

	Datetime(birth).format(text, 0, "Birth date: %Y.%m.%d / %A");
	dst.drawText(padding, lineHeight + padding, font, text, 0xffffff);
}

int32 onEvent(int32 action, int32 button, int32 x, int32 y) {
	inline scrollSpeed = 1.1;
	inline scrollStop = .5;
	static float64 scrollSpeedX = 0;
	static float64 scrollSpeedY = 0;
	static float64 scrollX = 0;
	static float64 scrollY = 0;
	static int32 mouseX = 0;
	static int32 mouseY = 0;

	if (action == Window.KEY_RELEASE && button == Window.KEY_CODE_ESC) {
		// exit when releasing escape key
		return Window.quit();
	}
	else if (action == Window.KEY_RELEASE && button == ' ') {
		scrollSpeedX = scrollStop;
		scrollSpeedY = scrollStop;
		scrollX = 0;
		scrollY = 0;
	}
	else if (action == Window.MOUSE_PRESS && button == 1) {
		scrollSpeedX = scrollStop;
		scrollSpeedY = scrollStop;
		mouseX = x;
		mouseY = y;
	}
	else if (action == Window.MOUSE_MOTION && button == 1) {
		static if (struct(scrollSpeed) != null) {
			scrollX += scrollSpeedX = mouseX - x;
			scrollY += scrollSpeedY = mouseY - y;
		} else {
			scrollX += mouseX - x;
			scrollY += mouseY - y;
		}
		mouseX = x;
		mouseY = y;
	}
	else if (action == Window.WINDOW_INIT) {
		scrollSpeedX = scrollStop;
		scrollSpeedY = scrollStop;
	}

	if (Float64.abs(scrollSpeedX) >= scrollStop || Float64.abs(scrollSpeedY) >= scrollStop) {
		img.drawBiorhythm(Timestamp(birthdate), now.add(scrollX, Timeunit.Hours), 30, 7);
		static if (struct(scrollSpeed) != null) {
			scrollX += scrollSpeedX /= scrollSpeed;
			scrollY += scrollSpeedY /= scrollSpeed;
			// delay next scroll animation
			return Window.timeoutFps(60);
		}
	}
	return Window.timeoutMax;
}
/* References:
http://lastunicorn.github.io/BioCalcJs/demo/index.html
https://github.com/lastunicorn/BioCalcJs/blob/gh-pages/demo/libraries/BiorhythmControls/BiorhythmControls.js#L410
// */
