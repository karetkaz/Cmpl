// Simple Calculator with no precedence
// double click result to clear it

Image offs = Image(300, 400, 32);
int onEvent(int action, int button, int x, int y);
offs.show(onEvent);
offs.destroy();

inline "/cmplGfx/lib/micro.ui.ci";
const FormatFlags format = {
	precision: 10;
};
char result[256] = {'0', 0};
char operation = 0;
float64 value = 0;
bool clear = true;

bool onClear(Button this);
bool onVal(Button this);
bool onOp(Button this);

struct CalcResult: Button {
	void onDraw(CalcResult this, Image offs, Rect rect, const Style style&) {
		offs.fillRect(rect, style.hoveredColor);
		Rect textRect = {};
		rect.adjust(-style.padding);
		style.font.clipText(&textRect, this.text);
		Style.align(rect, &textRect, Style.right);
		offs.drawText(textRect, style.font, this.text, style.textColor);
	}
}

CalcResult btnResult = {
	text: result;
	focusable: false;
	onReset: onClear;
};

Button buttons[] = {
	{text: "7", onClick: onVal}, {text: "8", onClick: onVal}, {text: "9", onClick: onVal}, {text: "/", onClick: onOp},
	{text: "4", onClick: onVal}, {text: "5", onClick: onVal}, {text: "6", onClick: onVal}, {text: "*", onClick: onOp},
	{text: "1", onClick: onVal}, {text: "2", onClick: onVal}, {text: "3", onClick: onVal}, {text: "-", onClick: onOp},
	{text: "0", onClick: onVal}, {text: ".", onClick: onVal}, {text: "=", onClick: onOp}, {text: "+", onClick: onOp}
};

Style style = {
	align: Style.center;
	fmtInt: { padLen: 4 };
	fmtFlt: { padLen: 4, precision: 2 };
};
Widget ui = Widget(offs, style, btnResult
	, Layout.Custom(4, Style.fill, ...buttons)
).margin(Style.fill, 0, 0).padding(0);

int onEvent(int action, int button, int x, int y) {
	if (action == Gui.KEY_RELEASE && button == 27) {
		// exit when releasing escape key
		return -1;
	}
	if (!ui.onEvent(action, button, x, y)) {
		return 0;
	}
	if (action == Gui.WINDOW_INIT) {
		ui.setFocused(null);
	}
	return ui.draw();
}
bool onClear(Button this) {
	result.append(0, "0");
	operation = 0;
	value = 0;
	clear = true;
	return true;
}
bool onVal(Button this) {
	int len = length(result);
	if (clear) {
		clear = false;
		len = 0;
	}
	result.append(len, this.text);
	return true;
}
bool onOp(Button this) {
	assert(this.text.length() == 1);
	if (clear) {
		// keep the last clicked operator
		operation = this.text[0];
		return true;
	}

	if (operation == '=' || operation == 0) {
		value = float64(result);
		operation = 0;
	}
	else if (operation == '+') {
		value += float64(result);
		operation = 0;
	}
	else if (operation == '-') {
		value -= float64(result);
		operation = 0;
	}
	else if (operation == '*') {
		value *= float64(result);
		operation = 0;
	}
	else if (operation == '/') {
		value /= float64(result);
		operation = 0;
	}

	// check for errors (not a number)
	if (value == value) {
		result.append(0, value, format);
	} else {
		result.append(0, "Error");
	}

	// make sure all operators were handled
	assert(operation == 0);
	operation = this.text[0];
	clear = true;
	return true;
}
