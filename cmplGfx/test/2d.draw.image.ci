/* image draw demo
 * click mouse button 1 and drag mouse to draw the image
 * click mouse button 2 to draw only a region from the original image
 * click mouse button 3 to draw the image tiled
 * click mouse button 1+2 to draw the image zoomed
 * press space bar to toggle fade out animation
 */
Image offs = Image(1440, 900, 32);
Image back = Image("asset/image/forest.jpg", 32);
Image draw = Image("asset/image/david.png", 32);

uint32 lut[256] = {};
for (int idx = 0; idx < 256; idx += 1) {
	int rgb = clamp_s8(idx - 10);
	lut[idx] = uint32(_argb(idx, rgb, rgb, rgb));
}

int onEvent(int action, int button, int x, int y) {
	static int64 millis = 0;
	static const Rect roi = {
		x: 30;
		y: 30;
		w: 120;
		h: 120;
	};
	if (action == Gui.KEY_PRESS) {
		if (button == 27) {
			return -1;
		}
		if (button == ' ') {
			// toggle animation
			if (millis == 0) {
				millis = System.millis();
			} else {
				millis = 0;
			}
		}
		if (button == 's') {
			trace("dumping screen");
			offs.saveBmp("temp/dump.bmp", 0);
		}
	}
	if (action == Gui.FINGER_MOTION) {
		action = Gui.MOUSE_MOTION;
	}
	if (action == Gui.MOUSE_PRESS) {
		offs.resize(null, back, null, 1);
		action = Gui.MOUSE_MOTION;
	}
	if (action == Gui.MOUSE_MOTION) {
		if (button == 1) {
			offs.copy(x, y, draw, null);
		}
		if (button == 2) {
			offs.copy(x, y, draw, roi);
		}
		if (button == 3) {
			Rect to = {
				x: 0;
				y: 0;
				w: x;
				h: y;
			};
			offs.resize(to, draw, null, 1);
		}
		if (button == 4) {
			offs.tile(x, y, draw, null);
		}
		if (button == 6) {
			offs.tile(x, y, draw, roi);
		}
	}
	if (millis != 0) {
		inline animateSpeed = 20;
		int64 now = System.millis();
		if (now - millis > animateSpeed) {
			offs.colorMap(null, lut);
			millis = now;
		}
		return animateSpeed;
	}

	return 0;
}
onEvent(Gui.MOUSE_PRESS, 0, 0, 0);
offs.show(onEvent);
offs.destroy();
back.destroy();
draw.destroy();
