// Hue saturation brightness demo.

gxSurf back = gxSurf("asset/image/forest.png", 32);
gxSurf font = gxSurf.openFnt("asset/font/modern-1.fnt");
gxSurf offs = gxSurf(back.width(), back.height(), back.depth());

mat4f colorMatrix(float luminosity, float saturation, float hue) {

	static mat4f lumMatrix(float val) {
		return {
			x: vec4f(1., 0., 0., val);
			y: vec4f(0., 1., 0., val);
			z: vec4f(0., 0., 1., val);
			w: vec4f(0., 0., 0.,  1.);
		};
	}

	static mat4f satMatrix(float val) {
		//~ val between 0, 3
		const float inv = 1 - val;
		const float R = .213f * inv;
		const float G = .715f * inv;
		const float B = .072f * inv;

		return {
			x: vec4f(R + val, G, B, 0.);
			y: vec4f(R, G + val, B, 0.);
			z: vec4f(R, G, B + val, 0.);
			w: vec4f(0., 0., 0., 1.);
		};
	}

	static mat4f hueMatrix(float val) {
		//~ assert(val >= 0 && val <= 2 * Math.pi);
		inline lR = 0.213;
		inline lG = 0.715;
		inline lB = 0.072;
		float cV = val.cos();
		float sV = val.sin();
		return {
			x: vec4f(lR+cV*(1-lR)+sV*(   -lR), lG+cV*(   -lG)+sV*( -lG), lB+cV*( -lB)+sV*(1 - lB),0.);
			y: vec4f(lR+cV*(0-lR)+sV*(0.1430), lG+cV*(1 - lG)+sV*(0.14), lB+cV*( -lB)+sV*(-0.283),0.);
			z: vec4f(lR+cV*(0-lR)+sV*(lR - 1), lG+cV*(   -lG)+sV*(  lG), lB+cV*(1-lB)+sV*(    lB),0.);
			w: vec4f(0., 0., 0., 1.);
		};
	}

	mat4f result = mat4f(
		1, 0, 0, 0,
		0, 1, 0, 0,
		0, 0, 1, 0,
		0, 0, 0, 1
	);

	result = mul(result, lumMatrix(luminosity));
	result = mul(result, satMatrix(saturation));
	result = mul(result, hueMatrix(hue));
	return result;
}

int onEvent(int action, int button, int x, int y);
offs.show(onEvent);
offs.destroy();
back.destroy();
font.destroy();

int onEvent(int action, int button, int x, int y) {
	inline "lib/micro.ui.ci";

	static Slider brightness = {
		text: "brightness";
		minValue: -100;
		maxValue: 100;
	};
	static Slider saturation = {
		text: "saturation";
		minValue: -100;
		maxValue: 200;
	};
	static Slider hue = {
		text: "hue";
		minValue: 0;
		maxValue: 360;
	};
	static Slider time = {
		text: "time";
		enabled: false;
	};

	static Layout ui = Layout(offs, font, brightness, saturation, hue, time);
	bool redraw = action == Gui.WINDOW_INIT;

	if (uiEvent(ui, action, button, x, y)) {
		redraw = true;
	}
	else if (action == Gui.KEY_PRESS) {
		if (button == 27) {
			return -1;
		}
		if (button == '\r') {
			brightness.value = 0;
			saturation.value = 0;
			hue.value = 0;
			redraw = true;
		}
		if (button == ' ') {
			trace("dumping screen");
			offs.saveBmp("out/dump.bmp", 0);
		}
	}

	if (redraw) {
		uint64 start = System.millis();
		mat4f mat = colorMatrix(brightness.val() / 100., saturation.val() / 100. + 1, Math.radians(hue.val()));
		offs.resize(null, back, null, 1);
		offs.colorMat(null, mat.data);
		offs.drawHist(null, 0x00ffffff);

		time.value = System.millis() - start;
		ui.draw(30, 30);
	}

	return 0;
}
