// Brightness contrast gamma demo.

gxSurf back = gxSurf("asset/image/forest.png", 32);
gxSurf font = gxSurf.openFnt("asset/font/modern-1.fnt");
gxSurf offs = gxSurf(back.width(), back.height(), back.depth());

void brightnessContrastGamma(uint32 lut[256], int brightness, int contrast, int gamma) {
	float cVal = (256 + contrast) / 256f;
	float gVal = 100f / gamma;
	argb lut2[*] = lut;

	for (int idx = 0; idx < lut.length; idx += 1) {
		float bcg = idx / 256f;
		// apply gamma
		bcg = float.pow(bcg, gVal);
		// apply contrast
		bcg = cVal * (bcg - .5f) + .5f;
		// apply brightness
		bcg = bcg + brightness / 256f;

		int rgb = grayClamp(int(256 * bcg));
		lut2[idx].r = rgb;
		lut2[idx].g = rgb;
		lut2[idx].b = rgb;
		lut2[idx].a = 0;
	}
}

int onEvent(int action, int button, int x, int y);
offs.show(onEvent);
offs.destroy();
back.destroy();
font.destroy();

int onEvent(int action, int button, int x, int y) {
	inline "lib/micro.ui.ci";

	static Slider brightness = {
		text: "brightness";
		value: 0;
		minValue: -256;
		maxValue: 256;
	};
	static Slider contrast = {
		text: "contrast";
		value: 0;
		minValue: -512;
		maxValue: 512;
	};
	static Slider gamma = {
		text: "gamma";
		value: 100;
		minValue: 1;
		maxValue: 1000;
	};
	static Slider time = {
		text: "time";
		enabled: false;
	};

	static Layout ui = Layout(offs, font, brightness, contrast, gamma, time);
	bool redraw = action == Gui.WINDOW_INIT;

	if (uiEvent(ui, action, button, x, y)) {
		redraw = true;
	}
	else if (action == Gui.KEY_PRESS) {
		if (button == 27) {
			return -1;
		}
		if (button == ' ') {
			trace("dumping screen");
			offs.saveBmp("out/dump.bmp", 0);
		}
	}

	if (redraw) {
		uint64 start = System.millis();
		uint32 hist[256];
		uint32 lut[256];
		brightnessContrastGamma(lut, brightness.val(), contrast.val(), gamma.val());
		offs.resize(null, back, null, 1);
		offs.colorMap(null, lut, false);
		offs.calcHist(null, 0x00ffffff, hist);
		offs.drawLut(null, hist, lut);

		time.value = System.millis() - start;
		ui.draw(30, 30);
	}

	return 0;
}
