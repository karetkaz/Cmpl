/* Color levels demo.
 *
 * use 'tab' / 'shift' + 'tab' to select next control
 * use 'enter' / 'shift' + 'enter' to select next control
 * use '+' / '-' to adjust value of selected control (use shift for more precision)
 * use 'backspace' / 'shift' + 'backspace' to reset selected / every control value
 * use 'space' to press a button, toggle switch or reset slider value
 * use '[' or ']' to show and hide the controls
 *
 * drag the mouse left or right in the window to adjust selected slider value
 */

Image back = Image("../asset/image/forest.jpg", 32);
Image offs = Image(back);

void levels(argb lut[256], int32 brightness, int32 x0, int32 x1, int32 y0, int32 y1) {
	if (x1 == x0) {
		x1 = x0 + 1;
	}
	for (int32 idx = 0; idx < lut.length; idx += 1) {
		int32 val = y0 + (idx - x0) * (y1 - y0) / (x1 - x0);
		val = Int32.max(val, Int32.min(y0, y1));
		val = Int32.min(val, Int32.max(y0, y1));
		int32 rgb = sat_s8(val + brightness);
		lut[idx].r = rgb;
		lut[idx].g = rgb;
		lut[idx].b = rgb;
		lut[idx].a = idx;
	}
}
void levels(argb lut[256], int32 brightness, int32 output, int32 input) {
	int32 x0 = input > 0 ? 0 : -input;
	int32 x1 = input < 0 ? 255 : 255 - input;
	int32 y0 = output < 0 ? 0 : output;
	int32 y1 = output > 0 ? 255 : 255 + output;
	return levels(&lut, brightness, x0, x1, y0, y1);
}

bool syncInputOutput(Slider slider, float64 value);
bool showHideAdvanced(Slider slider, Rect rect&);

Histogram histogram = {
	enabled: false;
};
Histogram lookup = {
	lines: true;
	enabled: false;
	focusable: false;
};

DebugView debug = {
	focusable: false;
	measures: -1;
	draws: -1;
};

Switch chkAdvanced = {
	static bool onClick(Switch switch) {
		switch.invalidate();
		return Switch.onClick(switch);
	}

	text: "Advanced";
	onClick: onClick;
};

Slider sldBrightness = {
	text: "Brightness";
	minimum: -256;
	maximum: 256;
	stepSize: 1;
};

Slider sldInput = {
	text: "Input";
	minimum: -255;
	maximum: 255;
	stepSize: 1;
	onUpdate: syncInputOutput;
};
Slider sldInputMin = {
	text: " Input.min";
	maximum: sldInput.maximum;
	value: 0;
	stepSize: 1;
	onUpdate: syncInputOutput;
	onMeasure: showHideAdvanced;
};
Slider sldInputMax = {
	text: " Input.max";
	maximum: sldInput.maximum;
	value: sldInput.maximum;
	stepSize: 1;
	onUpdate: syncInputOutput;
	onMeasure: showHideAdvanced;
};

Slider sldOutput = {
	text: "Output";
	minimum: -255;
	maximum: 255;
	stepSize: 1;
	onUpdate: syncInputOutput;
};
Slider sldOutputMin = {
	text: " Output.min";
	maximum: sldOutput.maximum;
	value: 0;
	stepSize: 1;
	onUpdate: syncInputOutput;
	onMeasure: showHideAdvanced;
};
Slider sldOutputMax = {
	text: " Output.max";
	maximum: sldOutput.maximum;
	value: sldOutput.maximum;
	stepSize: 1;
	onUpdate: syncInputOutput;
	onMeasure: showHideAdvanced;
};

int32 onEvent(int32 action, int32 button, int32 x, int32 y) {
	if (action == Window.KEY_RELEASE && button == Window.KEY_CODE_ESC) {
		// exit when releasing escape key
		return Window.quit();
	}
	if (action < Window.EVENT_PROCESSED) {
		// action was not processed by the widget, do not redraw
		return Window.timeoutMax;
	}
	debug.startTimer();

	if (FxWidget.showOriginal(debug.getWidget())) {
		offs.copy(back);
		histogram.update(offs);
		return Window.timeoutMax;
	}

	levels(&lookup.data, sldBrightness.value, sldInputMin.value, sldInputMax.value, sldOutputMin.value, sldOutputMax.value);
	offs.colorMapRgb(0, 0, back, null, lookup.data);
	histogram.update(offs);
	return Window.timeoutMax;
}
bool syncInputOutput(Slider slider, float64 value) {
	bool result = Slider.onUpdate(slider, value);
	if (slider == sldInput) {
		result |= Slider.onUpdate(sldInputMin, value > 0 ? 0 : -value);
		result |= Slider.onUpdate(sldInputMax, value < 0 ? sldInput.maximum : sldInput.maximum - value);
		return result;
	}
	if (slider == sldOutput) {
		result |= Slider.onUpdate(sldOutputMin, value < 0 ? 0 : value);
		result |= Slider.onUpdate(sldOutputMax, value > 0 ? sldOutput.maximum : sldOutput.maximum + value);
		return result;
	}
	if (slider == sldInputMin || slider == sldInputMax) {
		result |= Slider.onUpdate(sldInput, 0);
		return result;
	}
	if (slider == sldOutputMin || slider == sldOutputMax) {
		result |= Slider.onUpdate(sldOutput, 0);
		return result;
	}
	return result;
}
bool showHideAdvanced(Slider slider, Rect rect&) {
	if (!chkAdvanced.selected) {
		// view is hidden
		rect.x1 = rect.x0;
		rect.y1 = rect.y0;
		return false;
	}
	return Slider.onMeasure(slider, &rect);
}

Window.show(offs, onEvent, FxWidget(
	histogram,
	lookup,

	null,
	sldBrightness,
	sldOutput,
	sldOutputMin,
	sldOutputMax,
	sldInput,
	sldInputMin,
	sldInputMax,
	chkAdvanced,

	null, debug
));
offs.destroy();
back.destroy();
