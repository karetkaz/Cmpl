/* Transform the geometry of an image using 4x4 matrix.
 *
 * use 'tab' / 'shift' + 'tab' to select next control
 * use 'enter' / 'shift' + 'enter' to select next control
 * use '+' / '-' to adjust value of selected control (use shift for more precision)
 * use 'backspace' / 'shift' + 'backspace' to reset selected / every control value
 * use 'space' to press a button, toggle switch or reset slider value
 * use '[' or ']' to show and hide the controls
 *
 * drag the mouse left or right in the window to adjust selected slider value
 */

Image back = Image("../asset/image/forest.jpg", 32);
Image offs = Image("../asset/image/texture_nature_01.png", back.width(), back.height(), 32);

/// compute the the transformation matrix to center/crop/fit an image to a new size.
mat4f fitMatrix(Image src, int newWidth, int newHeight, bool fitWidth, bool fitHeight, float64 maxScale, float64 maxAspect) {
	float64 scale = 1;
	float64 widthScale = newWidth / float64(src.width());
	float64 heightScale = newHeight / float64(src.height());

	if (fitWidth && fitHeight) {
		scale = Float64.min(widthScale, heightScale);
	}
	else if (fitWidth) {
		scale = widthScale;
	}
	else if (fitHeight) {
		scale = heightScale;
	}
	if (scale > maxScale) {
		scale = maxScale;
	}

	if (maxAspect > 0) {
		if (maxAspect > 1) {
			maxAspect = 1 / maxAspect;
		}
		float64 ratio = src.width() / float64(src.height());
		if (ratio > 1) {
			ratio = 1 / ratio;
		}
		// debug("new aspect", ratio);
		if (ratio > maxAspect) {
			scale *= Float64.sqrt(maxAspect / ratio);
		}
	}

	// calculate translation
	float64 dx = (newWidth - src.width() * scale) / 2;
	float64 dy = (newHeight - src.height() * scale) / 2;

	return {
		x: {x: 1 / scale, y: 0, z: 0, w: -dx / scale};
		y: {x: 0, y: 1 / scale, z: 0, w: -dy / scale};
		z: {x: 0, y: 0, z: 1 / scale, w: 0};
		w: {x: 0, y: 0, z: 0, w: 1};
	};
}
mat4f skewMatrix(float64 sx, float64 sy) {
	return {
		x: {x: 1, y: Float64.tan(sx), z: 0, w: 0};
		y: {x: Float64.tan(sy), y: 1, z: 0, w: 0};
		z: {x: 0, y: 0, z: 1, w: 0};
		w: {x: 0, y: 0, z: 0, w: 1};
	};
}

char filtering[][] = {
	"nearest",
	"linear",
	// TODO: Hermite, Gaussian, Bell, BSpline, Mitchel, Lanczos, Hanning
};
Switch quality = {
	text: "filtering";
	states: filtering;
	selected: 1;
	focusable: false;
};
Switch flipX = {
	text: "flip x";
};
Switch flipY = {
	text: "flip y";
};
Slider rotate = {
	text: "rotate";
	maximum: 360;
};
Slider skewX = {
	text: "skew x";
	minimum: -45;
	maximum: +45;
};
Slider skewY = {
	text: "skew y";
	minimum: -45;
	maximum: +45;
};
Slider scaleX = {
	text: "scale x";
	value: 1;
	minimum: .1;
	maximum: 5;
};
Slider scaleY = {
	text: "scale y";
	value: 1;
	minimum: .1;
	maximum: 5;
};
Slider translateX = {
	text: "translate x";
	minimum: -offs.width();
	maximum: +offs.width();
};
Slider translateY = {
	text: "translate y";
	minimum: -offs.height();
	maximum: +offs.height();
};

Switch fitWidth = {
	text: "fit width";
};
Switch fitHeight = {
	text: "fit height";
};
Slider imgRatio = {
	text: "ratio";
	enabled: false;
	onDrawSlider: null;
	value: float64(back.width()) / back.height();
	maximum: 1000;
};
DebugView debug = {
	focusable: false;
	measures: -1;
	draws: -1;
};

int32 onEvent(int32 action, int32 button, int32 x, int32 y) {
	if (action == Window.KEY_RELEASE && button == Window.KEY_CODE_ESC) {
		// exit when releasing escape key
		return Window.quit();
	}
	if (action < Window.EVENT_PROCESSED) {
		// action was not processed by the widget, do not redraw
		return Window.timeoutMax;
	}

	debug.startTimer();
	int32 padding = 50;
	Rect to = {
		x0: padding;
		y0: padding;
		x1: offs.width() - padding;
		y1: offs.height() - padding;
	};
	vec4f center = vec4f(back.width() / 2, back.height() / 2, 0, 0);
	mat4f mat = translation(center, 1)
		.mul(scale(vec4f(scaleX.value, scaleY.value, 0, 1), 1f))
		.mul(skewMatrix(Float64.radians(skewX.value), Float64.radians(skewY.value)))
		.mul(rotation(vec4f(0, 1, 0, 0), Float64.radians(flipX.selected * 180)))
		.mul(rotation(vec4f(1, 0, 0, 0), Float64.radians(flipY.selected * 180)))
		.mul(rotation(vec4f(0, 0, -1, 0), Float64.radians(rotate.value)))
		.mul(translation(vec4f(translateX.value, translateY.value, 0, 0), 1))
		.mul(translation(center, -1))
		.mul(fitMatrix(back, to.width(), to.height(), fitWidth.selected != 0, fitHeight.selected != 0, 1, 0));
	debug("transform", mat.m);
	offs.transform(to, back, mat, quality.selected);
	return Window.timeoutMax;
}

Window.show(offs, onEvent, FxWidget(
	quality,
	fitWidth, fitHeight,
	scaleX, scaleY,
	skewX, skewY,
	flipX, flipY,
	rotate,
	translateX, translateY,
	imgRatio,
	null, debug
));
offs.destroy();
back.destroy();

/* References:
todo: image scaling algorithms
https://en.wikipedia.org/wiki/Comparison_gallery_of_image_scaling_algorithms
https://en.wikipedia.org/wiki/Image_scaling

todo: perspective transformations
https://stackoverflow.com/questions/51691482/how-to-create-a-2d-perspective-transform-matrix-from-individual-components
https://www.pyimagesearch.com/2014/08/25/4-point-opencv-getperspective-transform-example/
https://developer.mozilla.org/en-US/docs/Web/CSS/transform
https://www.w3.org/TR/css-transforms/

https://stackoverflow.com/questions/12995149/css3-transform-skew-both-ways
https://dev.opera.com/articles/understanding-3d-transforms/
https://www.intmath.com/math-art-code/css-matrix-math.php
https://angrytools.com/css-generator/transform/
https://ramlmn.github.io/visualizing-matrix3d/
*/
